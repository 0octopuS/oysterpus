# 典型程序设计框架

## 1 面向对象化？

有的时候要用到类似C++里面的class类似的概念，把一堆原有的类包起来，然后通过特定的方法调用。

举个`Money`类型的例子，如果直接用`Int`，可能会迷失含义，出现使用`Money`相乘等错误。所以将`Int`类包裹在新类`Money`中，并且定义所有`Money`的处理函数（很自然的其他方法就无法直接处理`Money`类，这就形成了一个对象和对象方法）。

```Haskell
data Money = Money Int
  deriving Show

renderMoney :: Money -> String
renderMoney (Money cents) = show (fromIntegral cents / 100)

(+!) :: Money -> Money -> Money
(Money a) +! (Money b) = Money (a+b)

scale :: Money -> Double -> Money
scale (Money a) x = Money (round (fromIntegral a * x))

addVat :: Money -> Money
addVat m = m +! scale m 0.24
```

## 根据Cases

由于代数数据类型的特性，根据不同Cases来处理情况就变得很容易。常见的情况是定义一个可能Cases的集合，然后通过模式匹配来找到对应Cases。

考虑标准库的`NonEmpty`类型，`(:|)`是一个中缀运算符，使用方法列表构造中的`(:)`类似。

```Haskell
data NonEmpty a = a :| [a]
```

对`NonEmpty`和普通`List`之间进行转化，需要使用 `Maybe`类型来表示空的情况。

```Haskell
nonEmpty :: [a] -> Maybe (NonEmpty a)
nonEmpty [] = Nothing
nonEmpty (x:xs) = Just (x :| xs)

toList :: NonEmpty a -> [a]
toList (x :| xs) = x : xs
```

标准库中关于`NonEmpty`的`head`和`last`函数也是根据模式匹配，具体如下。

```Haskell
neHead (x :| _) = x
neLast (x :| []) = x
neLast (_ :| xs) = last xs
```

总之，如果能写出类型能代表值的所有可能情况，然后写函数来处理这些情况，代码就会简单又正确。

## Monoids 单子/幺半群

在函数式编程中，一个经常出现的模式是Monoids（不是Monad）。
期基本思想就是**将事物结合起来**。

### 结合性操作

很多函数和操作符是具有结合性的，最简单的类似于加法、乘法、`(.)`等。例如`(1 +  3) + (5 + 7) =1 + (3  + (5 + 7)) =1  + 3  +  5 + 7`。

但同时也有很多是不结合的，例如减法、平方等等。列表构造符号`(:)`就不是结合的，`True:(False:[])`是一个`[Bool]`，而`(True:False):[]`甚至不是一个类型。

除了操作符以外，函数也可以是结合性的，比如`min,max`操作。
只要满足以下两式相等就可以。

```Haskell
f x (f y z)
f (f x y) z
```

### 半群

从数学上来讲，结合性操作能够形成一个**半群**。Haskell中就有这样一个半群类`Semigroup`（在`Data.Semigroup`中定义）。

```Haskell
class Semigroup a where
  -- An associative operation.
  (<>) :: a -> a -> a
```

`List`是一个`Semigroup`的实例，用`(<>)`表示`(++)`。

```Haskell
[1] <> [2,3] <> [4]  ==>  [1,2,3,4]
```

有着多个结合性操作的类型通常不是`Semigroup`的一个实例，`Int`就是一个例子，它的操作`+,*,min,max`都是结合性操作。
在Haskell的标准库中，使用boxing的方法来处理它们。

```Haskell
data Sum a = Sum a
instance Num a => Semigroup (Sum a) where
  Sum a <> Sum b  =  Sum (a+b)

data Product a = Product a
instance Num a => Semigroup (Product a) where
  Product a <> Product b   =  Product (a*b)
```

## Monoids 幺半群

一个幺半群是有着一个单位元的半群。单位元就是一个“0”：某个和其他元素结合不会做任何改变的元素。

Haskell中也有一个类`Monoid`（在`Data.Monoid`模块中定义）。它已经依赖了`Semigroup`，所以只要再加一个单位元就行。

```Haskell
class Semigroup a => Monoid a where
  -- The neutral element
  mempty :: a
```

对于加法群、乘法群定义的单位眼如下

```Haskell
instance Monoid (Sum a) where
  mempty = Sum 0

instance Monoid (Product a) where
  mempty = Product 1

instance Monoid [] where
  mempty = []
```
如果是一个幺半群，那么对于一个程序员来说，也就是可以将某个东西保持不变，并且不用写括号，这在程序中是非常常见的。

### 幺半群用处

库函数可以被定义为对所有的`Monoid`类生效。我们需要单位元和结合性运算的原因是，如果要使用**`reduce`和`fold`**操作将多个元素折叠成一个值，我们就需要这两个特性。

```Haskell
mconcat :: Monoid a => [a] -> a
-- 可以用foldr (<>) mempty来表示
```

`mconcat` 的实现是这样的。首先需要`mempty`来处理空列表的情况（也就是返回的默认值）。

```Haskell
mconcat [] :: Sum Int          ==>  Sum 0
```

接着需要使用结合性操作来把`[x,y,z]`约简成一个独特的值，如果`(<>)`不是结合性的，那么对于`mconcat [x,y,z]`就可能会产生两个不同的值`(x<>y)<>z`和`x<>(y<>z)`。

最常用的`Monoid`函数是`foldMap`。

```Haskell
foldMap :: (Foldable t, Monoid m) => (a -> m) -> t a -> m
foldMap Max [1::Int,4,2]  ==>  Max 4
foldMap Product [1::Int,4,2]  ==>  Product 8
-- 我们需要 ::Int 来避免“Ambiguous type variable”错误（在print时发生）
-- 实际上就是foldMap' f xs = mconcat (map f xs)
```

实际上由于`(Monoid a, Monoid b) => Monoid(a,b)`，一个函数就能求出列表的最大值和乘积等。

```Haskell
foldMap (\x -> (Max x, Product x)) [1::Int,4,2]  ==>  (Max 4, Product 8)
```

!!! Note
    在自己的代码中不一定要用到`monoid`类型，只是可能读库代码的时候会很经常看到。

### 怎么使用这两个类？

之前给的`Semigroup`和`Monoid`不是实际的代码，由于一些历史原因和性能问题，真正的定义时

```Haskell
class Semigroup a where
  -- | 一个结合性操作
  (<>) :: a -> a -> a

  -- 使用<>将一个非空列表中的元素结合起来
  sconcat :: NonEmpty a -> a
  sconcat as = ... -- default implementation omitted

  -- 使用<>将一个值和它自身结合起来，重复n次
  stimes :: Integral b => b -> a -> a
  stimes n x = ... -- default implementation omitted

class Semigroup a => Monoid a where
  mempty  :: a

  mappend :: a -> a -> a
  mappend = (<>)

  -- 使用<>将一个列表中的元素结合起来
  mconcat :: [a] -> a
  mconcat = ... -- default implementation omitted
```

因此在定义某个属于`Semigroup`或`Monoid`的实例是，最小定义是

```Haskell
instance Semigroup MyType where
  x <> y = ...

instance Monoid MyType where
  mempty = ...
```

## 开放抽象和封闭抽象

什么时候该使用type，而什么时候用Typeclass呢？简单来所，如果在一开始就已经定好这个类型里的所有情况，那么就用type；如果后期还可能增加，就用Typeclass。

看一个具体的例子。`Vehicle`类可以是值`Car`或`Airplane`。使用代数数据类型的实现如下

```Haskell
data Vehicle = Car String | Airplane String

sound :: Vehicle -> String
sound (Car _) = "brum brum"
sound (Airplane _) = "zooooom"
```

而使用Typeclass的实现如下，`Car`或`Airplane`分别有自己的类型。

```Haskell
data Car = Car String
data Airplane = Airplane String

class VehicleClass a where
  sound :: a -> String

instance VehicleClass Car where
  sound (Car _) = "brum brum"

instance VehicleClass Airplane where
  sound (Airplane _) = "zooooom"
```

这两种方式实现的功能是相同的。我们把用`data`关键字实现的叫**封闭抽象**，代表所有情况的集合是封闭的，并且可以在同一个地方进行处理。而`class`关键词实现的叫**开放抽象**，也就是可以增加新的情况，即使是在其他的模块中。

显然开放抽象是更具有扩展性的，而封闭抽象则好在能够处理所有的情况（比如说要处理两个`Vehicle`类型是否相同）。

## 语言模型

有的是时候可能要实现一个小型程序语言，又叫嵌入式域特定语言（EDSL:Embedded Domain-Specific Language）。由于语言经常是使用迭代的代数数据类型，所以用Haskell解释、估值是很容易的。

看一个网络商店的例子，这个语言中有5个关键字，表达为一个有着5个值的Type。

```Haskell
data Discount = DiscountPercent Int         -- 百分比折扣
              | DiscountConstant Int        -- 常数折扣
              | MinimumPrice Int            -- 最低价格
              | ForCustomer String Discount -- 有条件的折扣
              | Many [Discount]             -- 对于某一行所有的数都进行折扣
```

使用一个函数`applyDiscount`来对这个语言进行翻译，输入参数为一个用户名字，一个价格类和一个折扣；返回一个最终价格。

```Haskell
applyDiscount :: String -> Int -> Discount -> Int
-- 对折扣类型进行匹配，并进行相应操作
applyDiscount _        price (DiscountPercent percent) = price - (price * percent) `div` 100
applyDiscount _        price (DiscountConstant discount) = price - discount
applyDiscount _        price (MinimumPrice minPrice) = max price minPrice
applyDiscount customer price (ForCustomer target discount)
    | customer == target  = applyDiscount customer price discount -- 达到标准则折扣
    | otherwise           = price
applyDiscount customer price (Many discounts) = go price discounts
  where go p [] = p
        go p (d:ds) = go (applyDiscount customer p d) ds
```

看具体的例子，如果想实现一个先-50%后-\$30，最小价格为\$35的折扣，那就可以使用以上定义的语言。

```Haskell
applyDiscount "Bob" 120 (DiscountPercent 50)
  ==> 60
applyDiscount "Bob" 60 (DiscountConstant 30)
  ==> 30
applyDiscount "Bob" 30 (MinimumPrice 35)
  ==> 35
applyDiscount "Bob" 120 (Many [DiscountPercent 50, DiscountConstant 30, MinimumPrice 35])
  ==> 35```
