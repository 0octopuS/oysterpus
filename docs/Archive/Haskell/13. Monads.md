# Monad

`Monad`和`Functor`一样都来源于范畴论，这章中将会使用例子的形式进行逐步讲解。

## 1 从例子入手

### 1.1 Example 1: Maybes

当我们使用`Maybe`的时候，代码有点乱糟糟的。看一个例子，首先把某个函数返回的`Maybe String`联合起来。当写到堆叠的`case`相关的地方的时候，代码看起来就很糟糕。

```Haskell
-- 尝试通过某个密码登录
-- `Just username` 代表成功, `Nothing` 则是失败.
login :: String -> Maybe String
login "f4bulous!" = Just "unicorn73"
login "swordfish" = Just "megahacker"
login _           = Nothing

-- 从一个用户那里获得相关联的secret
-- 不是所有的用户都有secret
secret :: String -> Maybe String
secret "megahacker" = Just "I like roses"
secret _            = Nothing

-- 如果有能够匹配的密码，则登录并返回用户的secret
stealSecret :: String -> Maybe String
stealSecret password =
  case login password of --这里就是非常messy的代码
    Nothing -> Nothing
    Just user -> case secret user of
                   Nothing -> Nothing
                   Just s -> Just ("Stole secret: "++s)
```

接下来我们尝试修改一个`pair`列表，我们使用一个返回`Maybe`的库函数`lookup :: (Eq a) => a -> [(a, b)] -> Maybe b`。该函数根据`key`寻找`value`，成功则返回`Just value`，否则`Nothing`。这里使用`case`嵌套`if-then-else`。

```Haskell
-- 当val比现有value大的时候进行重新设置
increase :: Eq a => a -> Int -> [(a,Int)] -> Maybe [(a,Int)]
increase key val assocs =
  case lookup key assocs  -- 这里开始类型判断的迭代，messy
  of Nothing -> Nothing
     Just x -> if (val < x)
                then Nothing
                else Just ((key,val) : delete (key,x) assocs)
```

这种代码模式是很常见的，并且重复相似的逻辑：如果任何备选结果是`Nothing`，那么整个结果都是`Nothing`。我们介绍一个性的运算符：**绑定运算符`(?>)`**。`(?>)`遇到`Nothing`则终止，否则接着跑下一步。

```Haskell
(?>) :: Maybe a -> (a -> Maybe b) -> Maybe b
-- 如果失败了，那么永远不运行下一个阶段
Nothing ?> _ = Nothing
-- 否则跑下一个阶段
Just x  ?> f = f x
```

绑定运算符就可以使之前的例子流线化。

```Haskell
stealSecret :: String -> Maybe String
stealSecret password =
    login password ?> -- login passward是Just吗？返回用户名(String)
    secret ?>  -- secret username有对应吗？返回Just secret(String)
    decorate   -- 
  where decorate s = Just ("Stole secret: "++s)
```

!!! Note
    `(?>)`总是向左绑定，也就是`op ?> f ?> g = (op ?> f) ?> g`，而`op ?> (f ?> g)`甚至不会被类型检查。

### 1.2 Example 2: Logging

我们进入另一个例子`logging`。类型`Logger`表示一个值和一个log信息的列表（存储计算过程中产生的值）。

```Haskell
-- Logger = [log] + val
data Logger a = Logger [String] a  deriving Show

getVal :: Logger a -> a 
getVal (Logger _ a) = a
getLog :: Logger a -> [String]
getLog (Logger s _) = s

-- 初始化操作:
nomsg :: a -> Logger a
nomsg x = Logger [] x        -- 只有一个值，没有msg

annotate :: String -> a -> Logger a
annotate s x = Logger [s] x  -- 有一个值和一个msg

msg :: String -> Logger ()
msg s = Logger [s] ()        -- 只有一个msg
```

现在定义一个`login`函数来记录不同用户名，和他们被尝试登录的密码。

```Haskell
-- 判断用户是否存在，返回Logger Bool类型
validateUser :: String -> Logger Bool
validateUser "paul.atreides" = annotate "Valid user" True
validateUser "ninja" = nomsg True
validateUser u = annotate ("Invalid user: "++u) False

-- 判断密码是否正确，返回Logger Bool类型
checkPassword :: String -> String -> Logger Bool
checkPassword "paul.atreides" "muad'dib" = annotate "Password ok" True
checkPassword "ninja"         ""         = annotate "Password ok" True
checkPassword _               pass       = annotate ("Password wrong: "++pass) False

login :: String -> String -> Logger Bool
login user password =
  let validation = validateUser user
  in if (getVal validation) -- 通过logger中的值进行判断
       then let check = checkPassword user password
            in Logger (getLog validation ++ getLog check) (getVal check) -- 增加log记录
       else validation 
```

抽象一下这里的模式：运行两个函数后分别得到两个`Logger Bool`类型，获取他们中的`msg`进行拼接，而`val`则采用后面那个的值。

```Haskell
(#>) :: Logger a -> (a -> Logger b) -> Logger b
Logger la a #> f = let Logger lb b = f a  -- 把左边Logger里的val直接喂给右边的函数，得到Logger lb b
                   in Logger (la++lb) b   -- 将两边的信息部分进行拼接
```

那么现在我们就可以使用`(#>)`运算符来修改`login`函数了。

```Haskell
login :: String -> String -> Logger Bool
login user password =
  validateUser user -- 得到第一个Logger Bool 
  #>
  \valid -> if valid then checkPassword user password
                     else nomsg False
  -- 使用lambda表达式，if-then-else和上面的函数相对应。
```

我们再看一点例子，定义一些计算函数的打印方程，然后在`compute`函数中使用`(#>)`来进行绑定。

```Haskell
-- square a number and log a message about it
square :: Int -> Logger Int
square val = annotate (show val ++ "^2") (val^2)

-- add 1 to a number and log a message about it
add :: Int -> Logger Int
add val = annotate (show val ++ "+1") (val+1)

-- double a number and log a message about it
double :: Int -> Logger Int
double val = annotate (show val ++ "*2") (val*2)

-- compute the expression 2*(x^2+1) with logging
compute :: Int -> Logger Int
compute x =
    square x 
    #> add
    #> double
compute 3
  ==> Logger ["3^2","9+1","10*2"] 20
```

那如果要在递归列表函数中使用`Logger`呢？我们重新定义一个运算符，以不同的方式对`Logger`进行绑定。

```Haskell
-- 不需要左边logger的val，直接丢掉
(##>) :: Logger a -> Logger b -> Logger b
Logger la _ ##> Logger lb b = Logger (la++lb) b
```

现在定义一个`filter`函数的`log`版本。

```Haskell
filterLog :: (Eq a, Show a) => (a -> Bool) -> [a] -> Logger [a]
filterLog f [] = nomsg []
filterLog f (x:xs)
    -- 满足条件则记录keeping，往下迭代。使用之前的#>，传入的这个lambda函数处理初始的[]。
   | f x       = msg ("keeping "++show x) ##> filterLog f xs #> (\xs' -> nomsg (x:xs'))
   | otherwise = msg ("dropping "++show x) ##> filterLog f xs
```

### 1.3 Example 3: Keeping State

有的时候我们需要计算来改变某些共享的状态。来看银行账户的例子。

```Haskell
import qualified Data.Map as Map

-- Bank = (用户名 -> 余额)的Map
data Bank = Bank (Map.Map String Int)
  deriving Show
```

如果要进行存款，则首先输入用户名、存款数。然后通过`adjust`函数找到名字对应的存款数，将其修改为`(x+amount)`。

```Haskell
-- 库函数，将一个函数应用到Map上的某个key对应的val
Map.adjust :: Ord k => (a -> a) -> k -> Map.Map k a -> Map.Map k a

deposit :: String -> Int -> Bank -> Bank
deposit accountName amount (Bank accounts) =
  Bank (Map.adjust (\x -> x+amount) accountName accounts)
```

取款就比较复杂一点，需要处理用户不存在、用户钱不够等情况。

```Haskell
-- 库函数，如果key不存在则返回默认值
Map.findWithDefault :: Ord k => a -> k -> Map.Map k a -> a

withdraw :: String -> Int -> Bank -> (Int,Bank)
withdraw accountName amount (Bank accounts) =
  let -- 对于不存在的账户余额为0
      balance = Map.findWithDefault 0 accountName accounts
      -- 不能超出账户余额
      withdrawal = min amount balance
      -- 这里和存款差不多
      newAccounts = Map.adjust (\x -> x-withdrawal) accountName accounts
  in (withdrawal, Bank newAccounts) -- 返回取款值和新的bank
```

最后写个函数，从一个账户中取出最多100原，将钱分成两半存入两个账户中。

```Haskell
share :: String -> String -> String -> Bank -> Bank
share from to1 to2 bank =
  let (amount,bank1) = withdraw from 100 bank
      half = div amount 2
      -- carefully preserve all money, even if amount was an odd number
      rest = amount-half
      bank2 = deposit to1 half bank1
      bank3 = deposit to2 rest bank2
  in bank3
```

在Haskell中，当你对一个值进行串行更新时，同时也在旁边进行一些其他的计算，这样的代码经常出现。让我们重写`share`，这样我们就不需要引用银行本身了。我们可以再次使用相同的链式思想来完成这个任务。

```Haskell
-- BankOp a会将bank里的一个值进行转换，并且返回一个类型为a的值
data BankOp a = BankOp (Bank -> (a,Bank))

-- 在一个bank上跑bankop
runBankOp :: BankOp a -> Bank -> (a,Bank)
runBankOp (BankOp f) bank = f bank

-- 在一个bankop后面跑另一个bankop
(+>>) :: BankOp a -> BankOp b -> BankOp b
op1 +>> op2 = BankOp combined
  where combined bank = 
    let (_,bank1) = runBankOp op1 bank -- op1的结果在bank1里
    in runBankOp op2 bank1 -- 直接当成新的值放到op2里跑

-- 跑一个带参数的op，使用前面的op返回的结果。
(+>) :: BankOp a -> (a -> BankOp b) -> BankOp b
op +> parameterized = BankOp combined
  where combined bank = 
    let (a,bank1) = runBankOp op bank -- op1的结果在bank1里
    in runBankOp (parameterized a) bank1 -- 直接当成新的值放到op2里跑，并使用前面的a作为函数parameterized的参数

-- 存款的op
depositOp :: String -> Int -> BankOp ()
depositOp accountName amount = BankOp depositHelper
    -- 只是把之前的deposit函数做了一个BankOp类包裹，由于不需要返回值，所以只给了一个()
  where depositHelper bank = ((), deposit accountName amount bank)

-- 取款的op，它本身长得和BankOp就很像
withdrawOp :: String -> Int -> BankOp Int
withdrawOp accountName amount = BankOp (withdraw accountName amount)
```

让我们来看看怎么让这些操作进行链式工作。

```Haskell
Prelude> bank = Bank (Map.fromList [("edsger",10),("grace",50)])

-- 使用 +>> 

Prelude> runBankOp (depositOp "edsger" 1) bank
((),Bank (fromList [("edsger",11),("grace",50)]))
-- edsger存款1 + grace存款 1
Prelude> runBankOp (depositOp "edsger" 1 +>> depositOp "grace" 1) bank
((),Bank (fromList [("edsger",11),("grace",51)]))
-- edsger存款1 + grace存款 1+edsger取款11
Prelude> runBankOp (depositOp "edsger" 1 +>> depositOp "grace" 1 +>> withdrawOp "edsger" 11) bank
(11,Bank (fromList [("edsger",0),("grace",51)]))

-- 使用 +>
-- edsger取款5 + 存入grace账户
Prelude> runBankOp (withdrawOp "edsger" 5 +> depositOp "grace") bank
((),Bank (fromList [("edsger",5),("grace",55)]))
-- edsger取款100 + 存入grace账户（余额不够）
Prelude> runBankOp (withdrawOp "edsger" 100 +> depositOp "grace") bank
((),Bank (fromList [("edsger",0),("grace",60)]))
```

需要注意初始阶段的bank必须应用`runBankOp`，因为`BankOp`转换可能是复合的，所以把**定义操作**和**执行操作**分开总是更好的。

再接着定义之前的`share`操作。

```Haskell
-- 将数量均分到两个账户中
distributeOp :: String -> String -> Int -> BankOp ()
distributeOp to1 to2 amount =
-- 链条两个存款操作
  depositOp to1 half
  +>>
  depositOp to2 rest
  where half = div amount 2
        rest = amount - half

-- 将一个账户中的东西均分
shareOp :: String -> String -> String -> BankOp ()
shareOp from to1 to2 =
-- 链条一个取款和一个分布，需要使用参数
  withdrawOp from 100
  +>
  distributeOp to1 to2
```

## 2 Monad 介绍

### 2.1 抽象一个Monad

到现在为止，已经有了三个不同的链式操作。

```Haskell
(?>) :: Maybe a -> (a -> Maybe b) -> Maybe b
(#>) :: Logger a -> (a -> Logger b) -> Logger b
(+>) :: BankOp a -> (a -> BankOp b) -> BankOp b
```

就和`map,Functor`一样，也有一个类族尝试捕捉这种特性。这个类就`Monad`，它和`Functor, Foldable`一样接收一个kind为`* -> *`的构造函数。

```Haskell
class Monad m where
  (>>=) :: m a -> (a -> m b) -> m b

  -- 将一个普通值转换为monad
  return :: a -> m a
  -- 简单的链条（类似于之前不使用参数的##>）
  (>>) :: m a -> m b -> m b
  a >> b  =  a >>= \_ -> b     -- 这个默认实现其实就是忽略掉参数
```

## 2.2 复杂一点的链条，用do

如果写一个复杂一点的链条操作，可能会变成这样子

```Haskell
f = op1 >>= continue
  where continue  x   = op2 >> op3 >>= continue2 x
        continue2 x y = op4 >> op5 x y

-- 或者写好看一点
f = op1 >>= \x ->
    op2 >>
    op3 >>= \y ->
    op4 >>
    op5 x y
```

这实在是很不优雅，但是好在我们可以使用`do`，让它按序操作！

```Haskell
f = do x <- op1  -- 让x得到op1的结果
       op2
       y <- op3
       op4
       op5 x y
```

如下是对应规则

```Haskell
do x <- op a       ~~~>       op a >>= \x -> do ...
do op a            ~~~>       op a >> do ...
do let x = expr    ~~~>       let x = expr in do ...
do finalOp         ~~~>       finalOp
```

### 2.3 Maybe => Monad

直接可以给`Maybe`定义`Monad`实例，之前所有的记号`(?>)`都可以用`(>>=)`来代替。

```Haskell
instance  Monad Maybe  where
    (Just x) >>= k      = k x
    Nothing  >>= _      = Nothing

    return x            = Just x

-- Just "HELLO" >>= (\x -> return (length x)) >>= (\x -> return (x+1))
-- ==> Just 6
-- Just 2 >> Nothing
--   ==> Nothing
```

### 2.4 Logger => Monad（Applicative首次出现!）

Logger显然也可以这么做。但是因为Haskell的一些变化，必须实现`Functor`和`Applicative`才能实现`Monad`实例。

```Haskell
import Control.Monad

data Logger a = Logger [String] a  deriving Show

msg :: String -> Logger ()
msg s = Logger [s] ()

-- Functor实例简单的将函数应用到值上
  fmap f (Logger log x) = Logger log (f x)

-- 这是个对任何monad起效的Applicative实例，后面在讲
instance Applicative Logger where
  pure = return
  (<*>) = ap

-- Monad实例中的逻辑就是上面的#>
instance Monad Logger where
  return x = Logger [] x
  Logger la a >>= f = Logger (la++lb) b
    where Logger lb b = f a
```

这里就不需要之前的`nomsg`操作了，因为它被`return`代替了。之前的`annotate`也可以用`>>`实现。

```Haskell
nomsg :: a -> Logger a
nomsg x = return x

annotate :: String -> a -> Logger a
annotate s x = msg s >> return x
```

接下来重写一下之前的`compute`和`filterLog`。注意这里甚至可以使用`do`块！

```Haskell
compute x = do
  a <- annotate "^2" (x*x)
  b <- annotate "+1" (a+1)
  annotate "*2" (b*2)

filterLog :: (Show a) => (a -> Bool) -> [a] -> Logger [a]
filterLog f [] = return []
filterLog f (x:xs)
   | f x       = do msg ("keeping "++show x)
                    xs' <- filterLog f xs
                    return (x:xs') -- 漂亮的return
   | otherwise = do msg ("dropping "++show x)
                    filterLog f xs
```

### BankOp => Monad(State Monad!)

Haskell中的`State` monad(module Control.Monad.Trans.State of the transformers package)是对类似于`BankOp`类的一个泛化。它有两个类型参数`s,a`（类比之前的`(val,Bank)`）。

```Haskell
data State s a = State (s -> (a,s))

runState (State f) s = f s -- 执行操作

-- 覆盖之前的state，并制造()
put :: s -> State s ()
put state = State (\oldState -> ((),state))

-- 制造当前state
get :: State s s
get = State (\state -> (state,state))

-- 使用一个函数改变当前state，制造()
modify :: (s -> s) -> State s ()
modify f = State (\state -> ((), f state))

-- Functor 和 Applicative 的实例先跳过

instance Monad (State s) where 
  return x = State (\s -> (x,s))

  op >>= f = State h
    where h state0 = let (val,state1) = runState op state0
                         op2 = f val
                     in runState op2 state1
```

注意这里是对`(State s)`声明`Monad`的类型，也就是只给了`State`部分参数，这样它的kind就是一个`* -> *`。（`Monad`只能对接收一个类型参数的类型构造函数做定义！）可以类比`Maybe`的情况

```Haskell
class Monad m where
  (>>=) :: m a -> (a -> m b) -> m b

instance Monad Maybe where
  (>>=) :: Maybe a -> (a -> Maybe b) -> Maybe b

instance Monad (State s) where
  (>>=) :: State s a -> (a -> State s b) -> State s b
```

现在让我们看看`State`的工作实例，考虑`State Int a`作为一个简单的计数器。


```Haskell
-- 往当前计数器上+i
add :: Int -> State Int ()
add i = do old <- get  -- 获得当前计数器
           put (old+i) -- 输出新的计数器

runState (add 1 >> add 3 >> add 5 >> add 6) 0
  ==> ((),15)

example :: State Int Int
example = do add 3           -- 增加3
             value <- get    -- 获取现在的值init+3
             add 1000        -- 增加1000
             put (value + 1) -- 覆写现在的状态，value+1
             return value    -- 制造 value, 这里是init+3
runState example 1
  ==> (4,5)
```

注意到类型`State s a`和`BankOp`很类似，表示了某个状态的转换过程。初始化的状态必须用`runState`来执行所有的操作。

在处理一个列表的时候，一个state可以替代一个计数器参数，以下是如何用`state`记录最大值更新的例子。

```Haskell
findLargest :: Ord a => [a] -> State a ()
findLargest [] = return ()
findLargest (x:xs) = do
  modify (\y -> max x y)  -- 将当前状态更新为最大值
  findLargest xs          -- 接着处理剩下的列表

runState (findLargest [1,2,7,3]) 0  ==>  ((),7)
```

## 3 Monad 和 IO / Functor

### 3.1 Monad 和 IO

`IO`结构中的控制流全都是`monad`，让我们重新来看一下。

```Haskell
when :: Monad m => Bool -> m () -> m () -- 当b真时执行op
unless :: Monad m => Bool -> m () -> m () -- 当b假时执行op
replicateM :: Monad m => Int -> m a -> m [a] --重复n次，收集结果
replicateM_ :: Monad m => Int -> m a -> m () --重复n次，扔掉结果
mapM :: Monad m => (a -> m b) -> [a] -> m [b] --对每个列表元素执行操作
mapM_ :: Monad m => (a -> m b) -> [a] -> m () --对每个列表元素执行操作，扔掉结果
forM  :: Monad m => [a] -> (a -> m b) -> m [b] -- 和mapM一样，顺序换换
forM_ :: Monad m => [a] -> (a -> m b) -> m ()
```

正如你看到的，我们现在可以用`mapM`来对所有的monads进行操作。

```Haskell
mapM (\x -> if (x>0) then Just (x-1) else Nothing) [1,2,3]  ==>  Just [0,1,2]
mapM (\x -> if (x>0) then Just (x-1) else Nothing) [1,0,3]  ==>  Nothing

mapM (\x -> msg "increment" >> msg (show x) >> return (x+1)) [1,2,3]
  ==> Logger ["increment","1","increment","2","increment","3"] [2,3,4]

runState (mapM (\x -> modify (x+) >> return (x+1)) [1,2,3]) 0
  ==> ([2,3,4],6)
```

`instance Monad IO`是编译器内置，不能自己实现。真正的副作用可以被嵌入`monads`，我们将**操作的pure定义**和**执行操作**分开来了。`monad`的操作符也可以和`IO`很好的匹配。

```Haskell
printTwoThings :: IO ()
printTwoThings = putStrLn "One!" >> print 2

echo :: IO ()
echo = getLine >>= putStrLn

confirm :: String -> IO Bool
confirm question = putStrLn question >> fmap interpret getLine
    where interpret "Y" = True
          interpret _ = False
```


### 3.2 Monads 是一种Functor

`Monad`中还有一个有用的操作`liftM`。

```Haskell
-- 直接把操作应用到值上
-- 联想一下fmap?
liftM :: Monad m => (a->b) -> m a -> m b
liftM f op = do x <- op
                return (f x)

liftM negate (Just 3)
  ==> Just (-3)
```

所以定义`Functor`实例非常容易，就是`fmap = liftM`由于每个`Monad`都需要是一个`Functor`，因此现代Haskell偏好使用`fmap`来替代`liftM`。


## 4 其他语言中的monad

### 4.1 Maybe的对应 => Option  

C++中的`std::optional<T>`和`Maybe`类似。

```C++
#include <iostream>
#include <optional>
using namespace std;

int main()
{
  std::optional<int> pp = 1; // 类型为int
  if (pp) {
      cout << *pp << endl; // 1
  }

  pp = nullopt; // 如果为空，名字为nullopt
  if (pp) {
      cout << *pp << endl; // 不输出
  }
}
```

来看一个对`Some`的`vector`进行操作的例子，使用迭代器找容器内部和指定值相同的元素。注意这里不需要对`Some`进行一个特殊提取，直接和指针的方式是相同的。如果为空的话，它指向的是空指针，比较就不会成功。

```C++
optional<Some> getSome(const std::vector<Some>& svec, int i)
{
  auto iter = std::find_if(svec.begin(), svec.end(), [i](const Some& s) {
      return s.some_i_ == i;
  });

  if (iter != svec.end()) {
      return *iter;
  }

  return nullopt;
}
```

换言之，在从C++中对一个`optional`类型做操作的时候，这些操作符都被重载了，直接应用在内部的值上。



Rust的`option`是一个枚举，枚举中每个元素可以存放对象或者不存放。

```Rust
enum Option<T> {
  Some(T),
  None,
} 
//getSome如果满足条件返回Some,不满足返回None。
fn getSome(b: bool) -> Option<i32> {
    if b {
        return Some(3);
    }
    return None;
}

fn main() {
    let b = false;
    if let Some(s) = getSome(b) {
        println!("hello.. {}", s);
    }
    else {
        println!("hello.. null");
    }
}
```

其他一些函数的对应

```Rust
pub fn unwrap(self) -> T // 调用内部值，None则panics
pub fn unwrap_or(self, default: T) -> T // None返回默认
pub fn unwrap_or_else<F>(self, f: F) -> T// 输出內部值的某个函数结果

```

### 4.2 liftM的对应

C++中好像没有一个内置的对应函数。

`liftM`在Rust中完全等于以下函数。

```Rust
pub fn map<U, F>(self, f: F) -> Option<U>
where
    F: FnOnce(T) -> U,
```

### 4.3 Stream 流

C++的`optional`类中定义的Monadic操作有

```C++
and_then(C++23)  如果optional有值，则返回给定函数的结果，否则返回空optional (公共成员函数)
==> >>= 

transform(C++23) 如果optional有值，则返回包含转换的值的optinal，否则返回空optional (公共成员函数)
==>  >> 

or_else(C++23)  如果optional有值，则返回它本身，否则返回给定函数的结果 (公共成员函数)
```

这些和Rust中给出的应该说是完全相同。

```Rust
///////////////////////////////////////////////////////////////////
pub fn and_then<U, F>(self, f: F) -> Option<U>
where
    F: FnOnce(T) -> Option<U>, // (>>=) :: Monad m => m a -> (a -> m b) -> m b
///////////////////////////////////////////////////////////////////

fn sq_then_to_string(x: u32) -> Option<String> {
    x.checked_mul(x).map(|sq| sq.to_string())
}

assert_eq!(Some(2).and_then(sq_then_to_string), Some(4.to_string()));  // 有值，对该值调用函数，得到平方
assert_eq!(Some(1_000_000).and_then(sq_then_to_string), None); // 有值，对该值调用函数，超出范围checked_mul失败，返回None
assert_eq!(None.and_then(sq_then_to_string), None); // 没有值，不调用

///////////////////////////////////////////////////////////////////
pub fn or_else<F>(self, f: F) -> Option<T>
where
    F: FnOnce() -> Option<T>, 
///////////////////////////////////////////////////////////////////

fn nobody() -> Option<&'static str> { None }
fn vikings() -> Option<&'static str> { Some("vikings") }

assert_eq!(Some("barbarians").or_else(vikings), Some("barbarians")); // Some("barbarians")有值，不被改变
assert_eq!(None.or_else(vikings), Some("vikings")); // None没有值，调用vikings，变成vikings
assert_eq!(None.or_else(nobody), None);  // None没有值，调用nobody，还是None
```

| **method**| **self**| **function input** | **function result**| **output**  |
|----------|---------|------------------|-------------------|---------|
| and_then | None    | (not   provided) | (not   evaluated) | None    |
| and_then | Some(x) | x                | None              | None    |
| and_then | Some(x) | x                | Some(y)           | Some(y) |
| or_else  | None    | (not   provided) | None              | None    |
| or_else  | None    | (not   provided) | Some(y)           | Some(y) |
| or_else  | Some(x) | (not   provided) | (not   evaluated) | Some(x) |

`and_then`就很类似`>>=`，可以反复使用向后绑定。


### 4.3 