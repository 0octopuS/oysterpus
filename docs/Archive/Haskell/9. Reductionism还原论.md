# Reductionism 还原论

## 1 惰性求值和纯净

Haskell是一个pure的函数式语言，也就是说`f x y`对于某个给定的`x`和`y`结果总是相同的（换言之`x`和`y`唯一确定了`f x y`的值。）这种性质也被叫做**引用透明性**（referential transparency）。Purity也代表没有任何的副作用，那么就不能让用户来输入`x`和`y`然后进行估值，否则`f`调用结果就不同了，这样就打破了引用透明性。

Haskell还是一个惰性求值的语言，也就是**值只有被需要的时候才被计算**。
比如说一个无穷循环，在Haskell中是可以使用的

```Haskell
f x = f x   -- 无穷循环
g x y = x
-- f 1 无穷估值
-- g 2 (f 1)  ==>  2 不用到第二个参数，因此f不会运行
```

!!! info "Lazy vs. non-strict"
    Haskell实际上用的是non-strict，和lazy不完全一样。
    Non-strictness代表reduction过程是**从外向内**的。具体来说如果有一个式子`(a+(b*c))`，那么首先计算外层的`+`，然后内层的`(b+c)`。而strict语言则从内层开始并向外求值。
    所以如果有一个表达式的估值结果是bottom（术语，代表无限循环或者错误），那对于strict的语言总是会对这个值先进行估计，然后将bottom向外传递。但是对于non-strict的方法， 那么一些子表达式就会被外面的reduction所消除，所以它们不会被评估，从而不会得到bottom。
    而lazy evaluation则意味着真正意义上的**用到才求值**。当估值引擎看到一个表达式，它建立一个thunk数据结构（thunk代表还没被估值的值），以及一个指向表达式的指针。当结果需要被估值出啦的时候，表达式的某个部分就会被计算，并且替代原有的thunk。
    在大部分的情况下lazy和non-strict是同义词，但也不全是。在Haskell中**模式匹配通常是strict的**，所以尝试要给模式匹配将会迫估值发生到找到一个匹配为止（使用头部`~`操作符可让模式匹配lazy）。strictness分析器还会寻找那些在估值时总是会被外层所需要的子表达式，并且提高他们的优先级。可以用`seq`原语来迫使表达表达式进行估值，无论是否被使用。

## 2 等式推理

参考透明度，即表达式对于相同的输入总是返回相同的值，是一个非常强大的属性，我们可以利用它来推理程序。考虑以下表达式

```Haskell
map (+1) . reverse . map (-1)
-- 实际上和reverse的效果完全相同
```

我们可以对其进行程序的推理，假设我们已知

```Haskell
map id          === id  -- map全等映射
map f . map g   === map (f.g)  -- map结合律
reverse . map f === map f . reverse  -- map 和reverse交换律
(+1) . (-1)x    === ((+1) ((-1) x))
                === ((+1) (x - 1))
                === (x - 1) + 1
                === x
                === id x        -- +1,-1组成全等映射
```

那么我们就可以做以下的程序推理

```Haskell
    map (+1) . reverse . map (-1)
=== map (+1) . (reverse . map (-1))    -- (.) 运算结合性
=== map (+1) . (map (-1) . reverse)    -- map 和reverse交换律
=== (map (+1) . map (-1)) . reverse    -- (.) 运算结合性
=== map ((+1) . (-1)) . reverse        -- map结合律
=== map id . reverse                   -- +1,-1组成全等映射
=== id . reverse                       -- map全等映射
=== reverse                            -- id全等映射
```

## 3 无穷列表

Laziness的好处可以通过**无穷列表**来演示。

```Haskell
Prelude> take 10 $ repeat 1
[1,1,1,1,1,1,1,1,1,1]
Prelude> take 20 [0..]
[0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19]
Prelude> take 10 . map (2^) $ [0..]
[1,2,4,8,16,32,64,128,256,512]
```

`cycle`函数将给定的列表反复重复，在解决旋转或者循环的时候很有用。

```Haskell
Prelude> take 21 $ cycle "asdf"
"asdfasdfasdfasdfasdfa"
Prelude> take 4 . map (take 4) . tails $ cycle "asdf"
["asdf","sdfa","dfas","fasd"]
```

`map`函数也可以直接作用在列表上，例如寻找第一个大于100的3的幂。

```Haskell
Prelude> head . filter (>100) $ map (3^) [0..]
243

-- ==> head (filter (>100) (1 : map (3^) [1..])) 
-- ==> head (filter (>100) (3 : map (3^) [2..])) 
-- ... 
-- ==> head (filter (>100) (81 : map (3^) [5..])) 
-- ==> head (filter (>100) (243 : map (3^) [6..])) 
-- ==> head (243 : filter (>100) (map (3^) [6..])) -- 过了filter，处理出了第一个列表元素，从外层估值，那么直接得到最后结果
-- ==> 243 
```

## 4 Haskell中lazy的实现方式

Haskell的估值非常简单。大多数其他编程语言（Java, C和Python）从内层向外估值，就会将传递给一个函数的所有参数进行估值。而Haskell时从外层向内的，过程如下

```Haskell
g :: Int -> Int -> Int
g x y = y+1
f :: Int -> Int -> Int -> Int
f a b c = g (a*1000) c


   f 1 (1234*1234) 2
=> f 1 1522756 2   -- Int -> Int -> Int -> Int
=> g  (1*1000) 2   -- Int -> Int -> Int 对f估值，而不是先对参数估值
=> g 1000 2        -- Int -> Int -> Int
=> 2 + 1           -- Int -> Int -> Int 对g估值，而不是先对参数估值
=> 3               -- Int
```

### 4.1 由模式匹配驱动的估值

在non-strict那里说过，Haskell不是完全lazy的。有的时候需要先对内部进行估值，才能知道匹配到哪个模式上。

来看一个例子`length (map not (True:False:[]))`。按照完全lazy的方法，估值应该是如下进行的，一个完全由外到内的过程。

```Haskell
length (map not (True:False:[]))
  ==> length (not True : not False : [])  -- evaluate call to map
  ==> length (False:True:[])              -- evaluate calls to not
  ==> 2
```

但是实际上Haskell中的过程是如下发生的

```Haskell
length (map not (True:False:[]))
  -- length有不同的模式匹配，比如[]和(fst:last)
  -- length的参数为map not (True:False:[])，必须要知道一个模式，所以对第一个先进行估值
  ==> length (not True : map not (False:[]))
  -- 现在我们有一个(:)，能够匹配了，由于外部开始，length先进行估值
  ==> 1 + length (map not (False:[]))
  -- 现在最外层的函数是(+)，它需要第二个参数，所以对内层进行估值。
  -- 发生了和之前完全一样的过程
  ==> 1 + length (not False : map not ([]))
  -- 最后一次也一样，要先估值内部的map not ([])，得到结果[]，才能进行length的模式匹配
  ==> 1 + (1 + length (map not []))
  ==> 1 + (1 + length [])
  ==> 1 + (1 + 0)
  ==> 1 + 1
  ==> 2
```

这种过程被称为**模式匹配迫使的估值**。当Haskell对某些被称为**弱首范式**（WHNF:weak head normal form，代表一个可以被模式匹配到的值）进行估值的时候，会出现这种过程。如果一个表达式在顶层能被估值，那么就称其在WHNF中。
可能有这几种情况
    1. 是一个常数
    2. 在顶层有一个构造函数，例如`False, Just(1+!), 0: filter f xs`。
    3. 是一个函数，例如`(\x -> 1+x)`。
不在WHNF中的最明显的一类表达式是**函数应用**。如果一个表达式由一个函数（不是构造函数）组成，应用于一些参数，它就不在WHNF中。我们必须对它进行评估，以便得到可被模式匹配的东西。

在前面的例子中我们不能对`length (map not (False:[]))`在顶层进行估计，因为`length`不在WHNF中。当我们对第二个等式`map`进行估值后，有了一个构造器，现在`length`就在WHNF中，就可以估值了。

在实际中，不止模式匹配会迫使估值，类似`(+)`这样的原语也会。我们可以说`(+)`对于两个参数都是strict的。

### 4.2 Sharing共享相同的值

只要给一个值命了名，那么它就是**共享(shared)**的。这意味着这个名字的每一次出现都指向同一个（可能是未估值的）表达式。当一个表达式被估值，这个名字出现的任何地方都能看到这个结果。

看一个例子。以下表达式如果按照从外到内的估值顺序，应该是这样被计算的

```Haskell
square x = x * x
square (2+2)
  ==> (2+2) * (2+2)   -- square估值
  ==>   4   * (2+2)   -- (*) 迫使左值进行估值
  ==>   4   *   4     -- (*) 迫使右值进行估值
  ==>      16         -- (*) 估值
```

那这个过程图形化表示的话就是
<figure><img src="Haskell.asset/10.1%20square1.svg"> </figure>

但是实际上`(2+2)`只会被计算一次，估值结果将会在两次出现时被共享。这是Haskell中的[图约简(graph reduction)](https://en.wikipedia.org/wiki/Graph_reduction)。

```Haskell
square (2+2)
  ==> (2+2) * (2+2)
  ==>   4   *   4
  ==>      16
```

<figure><img src="Haskell.asset/10.2%20square2.svg"> </figure>


但是要注意，一定要是相同的**名字**才能共享，不是相同的值。

```Haskell
-- 同名
f :: Int -> Int
f i = if i>10 then 10 else i
                  _______shared________
                 |                     |
f (1+1) ==> if (1+1)>10 then 10 else (1+1)
        ==> if 2>10 then 10 else 2
        ==> if False then 10 else 2
        ==> 2

-- 不同名，同值
g :: Int -> Int
g i j = if i>10 then 10 else j
                        ______no sharing_____
                       |                     |
g (1+1) (1+1) ==> if (1+1)>10 then 10 else (1+1)
              ==> if 2>10 then 10 else (1+1)
              ==> if False then 10 else (1+1)
              ==> (1+1)
              ==> 2
```

第二种不同名同值的情况将会被通过 另一种叫**公共子表达式消除**common subexpression elimination (CSE) 的手段进行优化。如果表达式出现在几个地方，代码将会被重整从而他们只会被计算一次。完全CSE化，可能会会影响程序的strictness/laziness，所以GHC只在特定的情况下进行CSE。可以自己手动做一下CSE。

```Haskell
  foo x = (bar x) * (bar x) -- bar x分别被计算
  foo x = let x' = bar x in x' * x'  -- bar x只计算一次并存储在x'中
```

## 5 如何才能更好的用无穷列表和Lazy方式

从上面的Lazy实现可以看到，有的时候实现的不好它就会向下求值。所以要写一些能够比较好处理无穷列表的辅助函数。

为了写一个能够转换无穷列表的函数，需要写一个只处理**有限前缀**的函数，使得能够输出一个`(:)`运算符，然后就能进行迭代了。例如以下：

```Haskell
everySecond :: [a] -> [a]
everySecond [] = []
everySecond (x:y:xs) = x : everySecond xs

take 10 (everySecond [0..])  ==>  [0,2,4,6,8,10,12,14,16,18]
```

一个好的启发式是：`head`函数能够轻易处理结果吗？来看两个不好的例子。
第一个是`mapTailRecursive`，在它的递归语句中没有给出一个部分结果，必须处理完整个输入，表达式才属于WHNF。

```Haskell
map :: (a -> b) -> [a] -> [b]
map _ []     = []
map f (x:xs) = f x : map f xs

mapTailRecursive :: (a -> b) -> [a] -> [b]
mapTailRecursive f xs = go xs []
    where go (x:xs) res = go xs (res++[f x]) --尾部递归，没有给出一个(:)，只能无限向下递归
          go []     res = res
```

第二个是`myDrop`，其中对无穷列表使用了`length`函数，而`length`本来就是对列表递归向下，所以将不会得到结果。

```Haskell
drop :: Int -> [a] -> [a]
drop 0 xs = xs
drop _ [] = []
drop n (x:xs) = drop (n-1) xs

myDrop :: Int -> [a] -> [a]
myDrop 0 xs = xs
-- 使用length函数
myDrop n xs = if n > length xs then [] else myDrop (n-1) (tail xs)
```

## 6 增加Strictness
Haskell中有个内部函数`seq a b`，能够对`b`进行估值，但是将`a`强行转成一个WHNF。

来讨论`foldl`函数，它和`foldr`的结合顺序相反。

```Haskell
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr f y []     = y
foldr f y (x:xs) = f x (foldr f y xs) -- 尾部递归，并作为第二个参数

foldl :: (a -> b -> a) -> a -> [b] -> a
foldl f z [] = z
foldl f z (x:xs) = foldl f (f z x) xs -- 尾部递归，并作为第一个参数

foldr (+) 0 [1,2,3]  ==>  1+(2+(3+0))
foldl (+) 0 [1,2,3]  ==>  ((0+1)+2)+3
```

### 5.1 对于单边Strict的运算符

类似`||,&&,++`等一系列符号有left strictness，也就是只要求左值进行运算。这些符号和`foldr`相性度很好，因为`foldr`的第一个参数是已知的`x`。

```Haskell
    foldr (&&) True [False,False,False]
==> False && (foldr (&&) True [False,False])
==> False
```

但是如果换成`foldl`，第一个参数变成了`f (f z x)`，那就变得要估值到结尾了。

```Haskell
    foldl (&&) True [False,False,False]
==> foldl (&&) (True&&False) [False,False]
==> foldl (&&) ((True&&False)&&False) [False]
==> foldl (&&) (((True&&False)&&False)&&False) []
==> ((True&&False)&&False)&&False
==> (    False    &&False)&&False
==>              False    &&False
==>                      False
```

### 5.2 对于双边Strict的运算符

对于双边都strict的运算符，例如`(+)`，两种`fold`都会展开成大量带有`(+)`的表达式。Haskell实现需要在内存中追踪整个表达式，这个问题被叫做**空间泄露**(space leak)。

```Haskell
    foldr (+) 0 [1,2,3]
==> 1 + foldr (+) 0 [2,3]
==> 1 + (2 + foldr (+) 0 [3])
==> 1 + (2 + (3 + foldr (+) 0 []))
==> 1 + (2 + (3 + 0))
==> 1 + (2 + 3)
==> 1 + 5
==> 6

    foldl (+) 0 [1,2,3]
==> foldl (+) (0+1) [2,3]
==> foldl (+) ((0+1)+2) [3]
==> foldl (+) (((0+1)+2)+3) []
==> ((0+1)+2)+3
==> (  1  +2)+3
==>       3  +3
==>          6
```

这个问题出现在laziness上，因为用到才估值，所以只能逐个展开。那如果能出来一个算一个，自然也就不需要追踪一个长表达式了。我们可以使用库函数`foldl'`。

```Haskell
    foldl' (+) 0 [1,2,3]
==> foldl' (+) (0+1) [2,3] 
==> foldl' (+) 1 [2,3] -- 逼迫第二个参数估值
==> foldl' (+) (1+2) [3]
==> foldl' (+) 3 [3]   -- 逼迫第二个参数估值
==> foldl' (+) (3+3) []
==> foldl' (+) 6 []    -- 逼迫第二个参数估值
==> 6
```

这就叫做**增加strictness**。这个库函数可以写成如下的定义，仅仅在原来的`foldl`上增加了第二个行的模式匹配。这样以后程序就必须要知道第二个参数的值，才能给`0`和`z`找到正确的匹配，那么第二个参数就会被强制估值。

```Haskell
-- foldl f z (x:xs) = foldl f (f z x) xs

foldl'Int :: (Int -> Int -> Int) -> Int -> [Int] -> Int
foldl'Int f z [] = z
foldl'Int f 0 (x:xs) = foldl'Int f (f 0 x) xs  
foldl'Int f z (x:xs) = foldl'Int f (f z x) xs
```

为了直接实现这个功能，Haskell直接引入了一个内置函数`seq a b`，估值结果为`b`，并将`a`估值到WHNF。为了演示发生了什么，这里引入一个新的值`undefined`，如果有东西被估值成WHNF就会抬起异常。

```Haskell
strictHead :: [a] -> a
strictHead xs = seq (last xs) (head xs)

Prelude> head [1,2,3]
1
Prelude> strictHead [1,2,3]
1
Prelude> head (1:2:undefined)
1
Prelude> strictHead (1:2:undefined) -- undefined被尝试转成WHNF
*** Exception: Prelude.undefined
Prelude> head [1..]
1
Prelude> strictHead [1..] -- 希望对last元素做操作，无止境递归
```

这是另一个`foldl'`定义，引入了一个新的共享变量`z'`，并且要求他被估值成一个WHNF。

```Haskell
-- foldl f z (x:xs) = foldl f (f z x) xs
foldl' :: (a -> b -> a) -> a -> [b] -> a
foldl' f z [] = z
foldl' f z (x:xs) = let z' = f z x
                    in seq z' (foldl f z' xs)
                    
   foldl' (+) 0 [1,2,3]
==> seq (0+1) (foldl' (+) (0+1) [2,3])  -- seq迫使第一个参数进行计算
          |                 |
          +-----sharing-----'
          |                 |
==> seq   1   (foldl' (+)   1   [2,3])  -- 第一个参数是WHNF了，seq消失
==> foldl' (+) 1 [2,3]
==> seq (1+2) (foldl' (+) (1+2) [3])
==> seq   3   (foldl' (+)   3   [3])
==> foldl' (+)   3   [3]
==> seq (3+3) (foldl' (+) (3+3) [])
==> seq   6   (foldl' (+)   6   [])
==> foldl' (+)   6   []
==> 6
```

## 7 newtype定义

之前提过`newtype`也能定义类，但只能有一个构造函数和一个域（box方法）。
`newtype`和`data`的内存构造完全不同。使用`data`将会引入一个中间层（构造函数），但是`newtype`是不会的。例如以下

```Haskell
code:                                 memory:

data Money = Cents Int                x --> Cents --> 100
x = Cents 100


newtype Money = Cents Int             x --> 100
x = Cents 100
```

具体的不同有这几个：
    1. `newtype`更高效，这个类型在编译完时候就消失了，但是会被彻底的进行类型检查，所以是在没有任何效率副作用下具有type safety的。
    2. `newtype`是strict的，只要在`x`是WHNF的情况下，`Money x`就是WHNF。
        ```Haskell
        -- 如果用data，那么 Cents undefined是在WHNF中
        Prelude> data Money = Cents Int
        Prelude> seq (Cents undefined) True
        True
        -- 如果用newtype, 那么 Cents undefined不在WHNF中
        Prelude> newtype Money = Cents Int
        Prelude> seq (Cents undefined) True
        *** Exception: Prelude.undefined```

只要是能够用单域单构造函数的情况就应该用`newtype`；或者是需要为一个type定义某个不同的typeclass实例的时候。

```Haskell
newtype Inverted = Inverted Int
  deriving (Show, Eq)

instance Ord Inverted where -- 定义不同名的Int比较方法
  compare (Inverted i) (Inverted j) = compare j i
```

## 8 循环数据结构

因为共享数据，循环的数据结构可以这么在内存中表示，就可以算没有代价了。
<figure><img src="Haskell.asset/10.3%20DAG4.svg"/></figure>

我们可以自己定义一个循环的数据结构，只要给一个变量某个名字，然后在值中指向这个名字本身。

```Haskell
  code                     memory

let xs = 1:2:xs      xs -> (1:) -> (2:) -+
 in xs                      ^            |
                            +------------+
```

## 9 Debug.Trace

即使Haskell是pure的编程员，有些时候一些impurity还是很有用的。

模块`Debug.Trace`中有函数`trace :: String -> a -> a`，能够深入看Haskell的估值过程。 表达式`trace "message" x`就和`x`一样，但是在估值的时候强制打印出`message`。例如

```Haskell
Prelude> import Debug.Trace
Prelude Debug.Trace> trace "a" True
a
True
Prelude Debug.Trace> trace "a" False || trace "b" True 
a
b
True
Prelude Debug.Trace> trace "a" True || trace "b" True
a
True
```

还有其他各种`trace`的变种，例如`traceShowId x = trace (show x) x`。