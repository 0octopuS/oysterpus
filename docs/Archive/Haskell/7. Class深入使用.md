# Class深入使用

在前两章中介绍了标准库中的Typeclasses和自定义Type（并通过`deriving`让其属于某个标准Typeclasses）。这一章主要介绍如何自己定义一个Typeclasses；以及自定义Type在某Typeclasses
中的具体行为。

## 定义Type在某个Typeclasses下的行为

如果用在定义Type的时候，用`deriving`声明属于某个Typeclasses，那么行为就完全按照标准库的语法。但是有的时候可能需要一些和标准不同的实现，此时可以使用`instance`关键字。

举例来说，Type`Person`中有工号、姓名、职位三个域。我们希望做到的效果是：如果工号和姓名都一样，则认为是同一个人。

```Haskell
data Person = Person {id::Int,name::String,position::String} deriving(Show)

peter1 = Person 1 "peter" "ceo"
peter2 = Person 1 "peter" "cto" -- peter2和peter1是同一人
peter3 = Person 2 "peter" "cfo" -- peter3和peter1是不同人
```

如果直接使用`deriving Eq`，那么在使用`(==),(\=)`运算符的时候将会逐个比较每个域，自然上面的三个人都是不同的。因此我们进行如下定义

```Haskell
instance Eq Person where
    (==) (Person id1 name1 pos1)(Person id2 name2 pos2) 
       = (id1 == id2) && (name1 == name2)
   -- 只比较工号和名字

-- ghci> peter1 == peter2
-- True
-- ghci> peter3 == peter2
-- False
```


## 自定义Typeclasses

### 新定义一个Typeclasses

通过`class`关键字来定义一个新的Typeclasses。以下定义了Typeclasses`Size`，参数为`a`，其中包含一个函数`size`。

```Haskell
class Size a where
  size :: a -> Int

-- Prelude> :t size
-- size :: Size a => a -> Int
```

注意上面的声明中没有具体的实现，这其实就是接口的定义（声明某种必须包含的行为）。根据`a`的不同，`size`的行为可以有所区别。

### 对属于该Typeclasses的不同类型进行功能实现

通过`instance`关键字来指定具体的函数实现。比如说给参数为`Int`和`List`时分别做实现。

```Haskell
instance Size Int where
  size x = abs x

instance Size [a] where
  size xs = length xs

-- ghci> size [True,False]
-- 2
```

这样就实现了一个简单的Typeclasses。还可以往里添加更多函数，在一个`instance`语句中可以对不同函数都进行实现。

```Haskell
class Size a where
  empty :: a
  size :: a -> Int
  sameSize :: a -> a -> Bool

instance Size (Maybe a) where
  empty = Nothing

  size Nothing = 0
  size (Just a) = 1

  sameSize x y = size x == size y

instance Size [a] where
  empty = []
  size xs = length xs
  sameSize x y = size x == size y
```

!!! Note
    只能定义所有类型参数为**不同type variable**的`instance`。
    - `instance Size (Either a b)` 合法定义
    - `instance Size (Either String a)` 不合法，`String`不是一个type variable
    - `instance Size (Either a a)` – 两个type variable相同
    - `instance Size [[a]]` – 不合法，`[a]` 不是type variable

### 默认实现方法

上面的例子中`sameSize`对于类型为`Maybe a`和`[a]`的实现是相同的，这个时候更好的方式是指定一个默认的方法。也就是在`class`语句中直接给出函数实现。

来看标准库中的`Eq`。这里对于`(==)`和`(\=)`的实现看着非常迷惑，两者互相为对方的`not`语句，形成了相互调用，那就没有实际的函数行为了。其实这里是**默认实现**，只要我们自己定义了两个方法中的任意一个，另一个操作符也就同样可用了。而用户定义的方法优先级比默认的高，这样就不会形成无限循环。（如果看`Eq`的文档中就能看到Minimal complete definition: either == or /=）。

```Haskell
class Eq a where
  (==) ::  a -> a -> Bool
  x == y  = not (x /= y)

  (/=) ::  a -> a -> Bool
  x /= y  = not (x == y)
```

再来看另一个标准库中`Ord`的例子，里面也有`compare, min, max`的默认实现都用到了`<=`运算符，而`<=`运算符本身用到了`compare`，同样是一个互相调用。所以实现`<=`或`compare`是必需的（`Ord`文档中Minimal complete definition: either compare or <=.）。

```Haskell
class  (Eq a) => Ord a  where
  compare              :: a -> a -> Ordering
  (<), (<=), (>=), (>) :: a -> a -> Bool
  max, min             :: a -> a -> a

  compare x y | x == y    = EQ
              | x <= y    = LT
              | otherwise = GT

  x <= y  = compare x y /= GT
  x <  y  = compare x y == LT
  x >= y  = compare x y /= LT
  x >  y  = compare x y == GT

  max x y | x <= y    =  y
          | otherwise =  x
  min x y | x <= y    =  x
          | otherwise =  y
```

## 3 层级架构

一个类可能是完全依靠另一个类的。比如上面的`Ord`，如果没有定义`(==)`，那么所有函数都无法工作了。所以最好让`Ord`类搭建在`Eq`类上面，也就是我们见到的

```Haskell
class  (Eq a) => Ord a  
```

同样`instance`也可能是完全依赖另一个`instance`的。比如定义一个`Pair`类，有一个类型参数`a`。定义`Eq (Pair a) instance`。发现程序报错，提示需要`(Eq a) instance`才能操作，也就是说需要所对比的所有类型参数都依赖于`Eq`。

```Haskell
data Pair a = MakePair a a
  deriving Show

instance Eq (Pair a) where
  (MakePair x y) == (MakePair a b)   =   x==a && y==b

error:
    • No instance for (Eq a) arising from a use of ‘==’
      Possible fix: add (Eq a) to the context of the instance declaration
    • In the first argument of ‘(&&)’, namely ‘x == a’
      In the expression: x == a && y == b
      In an equation for ‘==’:
          (MakePair x y) == (MakePair a b) = x == a && y == b
```

那可以加上**类型限制**，语法和之前函数的类型限制是相同的。

```Haskell
instance Eq a => Eq (Pair a) where
  (MakePair x y) == (MakePair a b)   =   x==a && y==b

-- Prelude> MakePair 1 1 == MakePair 1 1
-- True
```

这和C++中的继承派生概念不太一样，这个`instance`声明的含义是：如果`a`是`Eq`的一个`instance`，那么`Pair a`就是`Eq`的一个`instance`。

