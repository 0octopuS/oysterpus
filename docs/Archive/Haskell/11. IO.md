# IO

## 1 IO的基本使用

之前虽然一直在提到purity和side effect，但是Haskell却是一门很棒的指令式编程语言。当使用IO操作的时候只要将这些操作**按序**放在`do`块中。

```Haskell
questionnaire = do
  putStrLn "Write something!"
  s <- getLine
  putStrLn ("You wrote: "++s)
-- Prelude> questionnaire
-- Write something!
-- Haskell!
-- You wrote: Haskell!
```

也可以直接从网络上进行读取，例如以下程序通过HTTP协议从某个URL中获取部分内容并进行打印。

```Haskell
import Network.HTTP
import Control.Monad

main = do
  rsp <- simpleHTTP (getRequest "http://httpbin.org/base64/aGFza2VsbCBmb3IgZXZlcgo=")
  body <- getResponseBody rsp
  forM_ (words body) $ \w -> do
     putStr "word: "
     putStrLn w
```

### 1.1 IO类型探究

两个常用的IO函数类型如下。

```Haskell
Prelude> :t putStrLn
putStrLn :: String -> IO ()

Prelude> :t getLine
getLine :: IO String
```

类型`IO a`的值是一个**操作**，制造一个类型为`a`的**值**。因此`getLine`就制造一个`string`的IO操作。而`()`则是**单位类型**(unit type)，里面只包含一个值，也就是`()`本身。它最常使用的场景是在IO操作**不返回任何值**的时候。

`do`块实际上允许我们执行一系列有副作用的序列，然后最后制造出了一个值。

```Haskell
produceThree = do putStrLn "1"   -- 有副作用，制造()，被忽略
                  return    2    -- 没有副作用，制造2，被忽略
                  getLine        -- 有副作用，制造一个String，被忽略
                  return    3    -- 没有副作用，制造3，被传递
```

在写某个包含`do`块的函数的时候，总是返回值保持和最后一行的类型相同。

### 1.2 简易使用例子

在获取某个输入的时候经常使用`getLine`，使用`<-`操作符来捕捉IO操作的结果。

```Haskell
  s <- getLine  
```

如果需要给操作某些参数，可以写一个直接返回该操作的函数。

```Haskell
ask :: String -> IO String -- 参数为一个String
ask question = do
  putStrLn question
  getLine -- 直接返回getLine

Prelude> response <- ask "Who are you?"  -- 将ask的返回值命名为response
Who are you?                             -- ask中的putStrLn
The programmer.                          -- 用户输入
Prelude> response                        -- 用户输入的值
"The programmer."
Prelude> :t response                     -- response的类型是IO a中的a
response :: String
Prelude> :t ask                          
ask :: String -> IO String
Prelude> :t ask "Who are you?"           -- ask返回类型和getLine相同
ask "Who are you?" :: IO String
```

## 2 微妙的返回(return)

`return`在其它语言里是函数的返回值，但是在Haskell里是一个函数。它接收某个类型为`a`的值，将其转化为一个**操作**，返回一个值。

```Haskell
return :: a -> IO a 


produceThree :: IO Int  
produceThree = return 3 --接收Int，返回IO Int 

printThree :: IO ()
printThree = do
  three <- produceThree -- three为返回的Int
  putStrLn (show three) -- putStrLn接收String
```

由于这个函数制造了某个IO操作，所以它可以把某些值直接进行输出。例如以下这个例子判断用户的输入是否是"Y"，为了能够执行某个输出操作。这里`return`操作输出的直接是一个`Bool`类型。

```Haskell
yesNoQuestion :: String -> IO Bool
yesNoQuestion question = do
  putStrLn question
  s <- getLine
  return (s == "Y")
```

!!! Note
    和C或者Java不同，`return`不能代表终止某个函数。在`do`块中，最后一行决定了最后的返回值。例如以下操作返回2。
    ```Haskell
    produceTwo :: IO Int
    produceTwo = do return 1
                    return 2```
    另外由于`return`是一个函数，总是要记得写个括号。
    ```Haskell
    return (f x : xs)
    -- alternatively:
    return $ f x : xs```

## 3. `do`块和类型

再深入看一下`do`块的类型。`do`制造一个`IO a`的类型，因此最后一条语句**必须**是这个`IO a`。

```Haskell
foo :: IO a 
foo = do
  ...
  lastOp  -- IO a
```

如果是一些**带有参数**的情况，最后一条指令的类型为`b -> IO a`，那么这个函数的返回值就该是`IO a`。

```Haskell
foo :: typex -> typey -> IO a
bar x y = do 
  ...
  lastOp arg -- b -> IO a, arg::a
```

在使用`return`的时候也一样，和最后一条操作的类型必须相符合。

```Haskell
quux :: typea -> IO a
quux x = do
  ...
  return value  -- IO a,value :: a
```

!!! Note
    `do`的缩进是有点子怪的，一个重要原则就是 **`do`块里的东西起始列应该相同**。

## 4 控制结构

以下给出两个新的`IO`函数。

```Haskell
print :: Show a => a -> IO ()   -- 使用show函数进行打印
readLn :: Read a => IO a        -- 将读入内容使用read转换
```

可以使用guards或者if-then-else块进行结构控制。

```Haskell
printDescription :: Int -> IO ()
printDescription n
  | even n    = putStrLn "even"
  | n==3      = putStrLn "three"
  | otherwise = print n
```

同样也可以使用迭代结构，`readAndSum`读入n个数字打印他们的和。

```Haskell
readAndSum :: Int -> IO Int
readAndSum 0 = return 0
readAndSum n = do
  i <- readLn            -- 读入一个数字
  s <- readAndSum (n-1)  -- 递归:读入并算和
  return (i+s)           -- 制造结果
```

另外还有以下这些IO函数。

```Haskell
when :: Bool -> IO () -> IO () -- 当b真时执行op
unless :: Bool -> IO () -> IO () -- 当b假时执行op
replicateM :: Int -> IO a -> IO [a] --重复n次，收集结果
replicateM_ :: Int -> IO a -> IO () --重复n次，扔掉结果
mapM :: (a -> IO b) -> [a] -> IO [b] --对每个列表元素执行操作
mapM_ :: (a -> IO b) -> [a] -> IO () --对每个列表元素执行操作，扔掉结果
forM  :: [a] -> (a -> IO b) -> IO [b] -- 和mapM一样，顺序换换
forM_ :: [a] -> (a -> IO b) -> IO ()
```

## IO如何pure

类似于
`putString :: String -> IO()`这样返回某个操作的函数是pure的，每当`putString x`中的`x`相同时，结果总是相同的。换言之，一个操作实际上就是**一系列副作用的pure描述**，而只有**执行操作**会导致这些副作用。当一个Haskell函数允许的之后，只有一个操作会被执行，也就是`main ::IO()`，其他的操作都是被链接到`main`上进行运行的。

在GHCi中，如果一个表达式代表了一个操作，GHCi将会主动执行这个操作，例如以下的`print`。

```Haskell
Prelude> x = print 1   -- creates operation, doesn't run it
Prelude> x             -- runs the operation
1
Prelude> x             -- runs it again!
1
```

**操作就是一个值**，和任何其他的数字、列表、函数一样。我们可以用操作作为参数、返回值等。

```Haskell
choice :: IO x -> IO x -> IO x
choice a b =
  do putStr "a or b? "
     x <- getLine
     case x of "a" -> a
               "b" -> b
               _ -> do putStrLn "Wrong!"
                       choice a b

Prelude> choice (putStrLn "A!!!!") (putStrLn "B!!!!")
a or b? z
Wrong!
a or b? a
A!!!!                       
```

## IORef

还有一件Haskell做不到的事情是**可变状态**（mutable）。Pure函数不能读取那些可变状态，因为这样的话两次调用函数将会得到不一样的结果。

在Data.IORef模块中定义的`IORef`类型是一个对于某个类型为`a`的**可变值**的引用。

```Haskell
newIORef :: a -> IO (IORef a)                -- 创建一个包含一个值的IORef
readIORef :: IORef a -> IO a                 -- 将一个在IORef中的值取出
writeIORef :: IORef a -> a -> IO ()          -- 设置IORef中的值
modifyIORef :: IORef a -> (a -> a) -> IO ()  -- 使用一个纯净的方法修改包含在IORef中的值
```

以下将给出一些使用`IORef`的方法

```Haskell
Prelude> :m +Data.IORef -- 引入IORef模块
Prelude Data.IORef> myRef <- newIORef "banana" --创建新的IORef，值为banana
Prelude Data.IORef> readIORef myRef  -- 读取myRef中的值
"banana"
Prelude Data.IORef> writeIORef myRef "apple" -- 将apple写入到myRef中
Prelude Data.IORef> readIORef myRef -- 读取myRef中的值
"apple"
Prelude Data.IORef> modifyIORef myRef reverse -- 修改myRef中的值，传入的修改函数为倒置
Prelude Data.IORef> readIORef myRef
"elppa"
```

Haskell程序还是更偏向递归、参数和值语义的模式，几乎不会用到`IORef`。但可能很偶尔的情况下还是要用。
