# Type 类型

## 1 代数数据类型

### 1.1 定义类型

Haskell对于新定义的类型有一套体系，称为代数数据类型(algebraic datatypes)。语法如下，`(=)`之前的部分表示**类型**，`(=)`后面的部分是**值构造函数**。它们指定了这个类型可以拥有的不同值。`(|)`被理解为“或”。

```Haskell
data Bool = True | False  -- Bool类型可以有True或False的值。
data Ordering = LT | EQ | GT  -- Ordering类型可以拥有LT或EQ或GT的值
```

!!! Note
    类型名称和值构造函数都必须是**大写**。

通过这个方法就可以定义自己的类型，比如以下`Color`的定义。

```Haskell
-- 定义一个有三个值的类型
data Color = Red | Green | Blue

-- 定义一个输入参数为Color的函数，并使用模式匹配
rgb :: Color -> [Double]
rgb Red = [1,0,0]
rgb Green = [0,1,0]
rgb Blue = [0,0,1]
```

此时`Red, Green, Blue`就是`Color`类型中的三个值，也可以新定义变量赋值为这几个值。

```Haskell
ghci> :t Green
Green :: Color
ghci> :t [Red,Blue,Green]
[Red,Blue,Green] :: [Color]
ghci> newcolor = Red
ghci> :t newcolor
newcolor :: Color
ghci> rgb Red
[1.0,0.0,0.0]
```

上面所举的例子中`Bool, Ordering, Color`都只包含了一系列的常量，这种类型其实就是其他语言中的**枚举**(enum)。

### 1.2 Fields and Constructor 域和构造器

但同时有的时候不止需要常量，而是需要能够组合其他的类型。例如如下定义一个类型`Shape`，如果是`Circle`则使用中心点坐标和标记$(x,y,r)$表示；如果是`Rectangle`则使用左上/右下点坐标$(x_1,y_1,x_2,y_2)$表示。

```Haskell
data Shape = Circle Float Float Float | Rectangle Float Float Float Float 
```

我们把每个`Circle`和`Rectangle`叫做**构造函数**，而后面的`Float Float Float`则叫做**域**（也可以认为就是参数）。首先看一下构造器的类型，它们的定义看上去就像是一个普通函数。

```Haskell
ghci> :t Circle  
Circle :: Float -> Float -> Float -> Shape  
-- 接收三个Float， 输入一个Shape类型
ghci> :t Rectangle  
Rectangle :: Float -> Float -> Float -> Float -> Shape  
-- 接收四个Float， 输入一个Shape类型
```

那当然使用方式也就和函数一模一样，如果想要定义一个变量
可以直接使用类似`c = Circle 1 2 3`这样的语句。来看一个使用`Shape`作为参数的函数例子。

```Haskell
surface :: Shape -> Float  -- 计算表面积
surface (Circle _ _ r) = pi * r ^ 2   -- 圆形面积公式
surface (Rectangle x1 y1 x2 y2) = (abs $ x2 - x1) * (abs $ y2 - y1)   -- 方形面积公式
```

!!! Note
    用**模式匹配**可以对应到Type中的不同类型，同时还可以访问它们的每个域内容。注意需要**指明构造器**，用一个**括号**将所有域括起来。

### 1.3 Deriving 衍生于

刚刚定义了的`Shape`类型，在声明新变量的时候出现了一个报错。这是因为`print`方法只接受`Show`类型类，而`Shape`不是其中的一个。

```Haskell
ghci> c = Circle 1 2 3
ghci> c

<interactive>:33:1: error:
    ? No instance for (Show Shape) arising from a use of ‘print’
    ? In a stmt of an interactive GHCi command: print it

ghci> :t print
print :: Show a => a -> IO ()
```

这时候就需要把`Shape`类型归档到`Show`类型类中，所使用的关键字是`deriving`

```Haskell
data Shape = Circle Float Float Float | Rectangle Float Float Float Float deriving (Show)  
```

再次尝试将它们打印出来，可以看到如下结果。这种行为类似Python中直接把一个复杂列表打印出来，`Show`是逐个去处理`Shape`中的每一个域。

```Haskell
ghci> Circle 1 2 3
Circle 1.0 2.0 3.0
```

### 1.4 为什么叫代数数据类型？

为什么这些数据类型被称为代数型？这是因为从理论上讲，每个数据类型都可以是**构造函数的和**，而每个构造函数都是**域的积**。
考虑一些数学上的对应
 **Haskell**                        | **Math** | **Notes**
------------------------------------|---------------|--------------------------------------------------
 data Void                          | $0$             | 不可能构建这种类型的术语（需要LANGUAGE EmptyDataDecls）。
 data Unit = Unit                   | $1$             |  只有一个术语的类型
 data Bool = True | False           | $1+1$           | 可就是 2 (3, 4, ...)
 data Maybe a = Just a | Nothing    | $a+1$           | 把 “++” 叫做 “Either”
 data Either a b = Left a | Right b | $a+b$           |
 data (a, b) = (a, b)               | $a\times b$     | 把 “××” 叫做 “And”
 a -> b                             | $b^a$           |

## 2 类型参数

在列表介绍了类型参数和参数化多态性。此后，我们又看到了其他参数化类型，如 `Maybe` 和 `Either`。现在我们将学习如何定义我们自己的参数化类型。

首先来看`Maybe`的定义，其中的`a`是一个**类型变量**。这样就定义了一个能够支持类型多态的新类型。

```Haskell
data Maybe a = Nothing | Just a
```

也可以给定多个变量，每个值可以只要求一小部分。

```Haskell
data Either a b = Left a | Right b
```

!!! Note
    1. 类型参数应该是小写开头，`Maybe a`,`Maybe theType`都行。
    2. 构造函数和类型可以有着相同的名字，例如`data Pair a = Pair a a`。但是最好不要，因为含义可能会混。例如以下这个报错例子
        ```Haskell
        --尝试把类型名当成值来用
        Prelude> Maybe
        <interactive>:1:1: error:
            • Data constructor not in scope: Maybe
        -- 尝试把值当成类型名来用
        Prelude> undefined :: Nothing
        <interactive>:2:14: error:
            Not in scope: type constructor or class ‘Nothing’```

## 3 递归类型

前面定义的类型都是定长的，但类似于`List`这样的类型可能需要支持不定长的变量，那么对这类类型的定义需要使用递归。

以下这个例子中定义了整数列表类`IntList`。在第二个构造函数`Node`中，参数为一个`Int`和一个`IntList`本身。

```Haskell
data IntList = Empty | Node Int IntList
  deriving Show

ihead :: IntList -> Int -- 返回列表头
ihead (Node i _) = i

itail :: IntList -> IntList -- 返回除了列表头的剩余部分
itail (Node _ t) = t

ilength :: IntList -> Int -- 返回列表长度
ilength Empty = 0
ilength (Node _ t) = 1 + ilength t

-- Prelude> ihead (Node 3 (Node 5 (Node 4 Empty)))
-- 3
-- Prelude> itail (Node 3 (Node 5 (Node 4 Empty)))
-- Node 5 (Node 4 Empty)
-- Prelude> ilength (Node 3 (Node 5 (Node 4 Empty)))
-- 3
```

在上面的例子上加上类型参数，就形成了通用列表（注意`List a`要加括号）。

```Haskell
data List a = Empty | Node a (List a)
  deriving Show
```

那么二叉树等结构定义也非常简单

```Haskell
data Tree a = Node a (Tree a) (Tree a) | Empty
```

## 4 记录语法

如果有一些域经常被访问，那么定义一个帮助读这些域的函数总是好的。例如一个`Person`域可能有多个字段，其中职业字段经常被访问。那么可以使用**记录语法**来指明每个域的名称。

```Haskell
data Person = MkPerson { name :: String, age :: Int, town :: String, state :: String, profession :: String}
  deriving Show
```

在定义的时候还是可以和原来按序定义，也可以指明名称。`Show`此时就会显示出每个域的记录名来。

```Haskell
Prelude> MkPerson "Jane Doe" 21 "Houston" "Texas" "Engineer"
MkPerson {name = "Jane Doe", age = 21, town = "Houston", state = "Texas", profession = "Engineer"}

Prelude> MkPerson {name = "Jane Doe", town = "Houston", profession = "Engineer", state = "Texas", age = 21}
MkPerson {name = "Jane Doe", age = 21, town = "Houston", state = "Texas", profession = "Engineer"}
```

这些记录名可以作为**访问函数**。此时访问这些域是不需要寻找，代价很小的，类似于字典那样。

```Haskell
Prelude> :t profession
profession :: Person -> String
Prelude> profession (MkPerson "Jane Doe" 21 "Houston" "Texas" "Engineer")
"Engineer"
```

那么要写筛选函数的话就只用调用上面访问函数。

```Haskell
query :: [Person] -> [Person]
query []     = []
query (x:xs)
  | state x == "Finland" && profession x == "Engineer" =
      x : query xs
  | otherwise = query xs
```

## 5 其他定义类型的方式

除了`data`关键字外，还可以用`newtype`和`type`来定义类型。
`newtype`和`data`几乎相同，但是只能有**一个构造函数、一个域**。
`type`则可以用来定义**类型别名**，比如最经典的

```Haskell
type String = [Char]
```
