# Funtors 函子

## 1 函子介绍

### 1.1 保持结构

在Haskell中使用类型多态的函数通常是能够保持结构的，我们以`map`来举例。它接受一个类型`a`到类型`b`的转换函数，以及一个类型为`a`的列表，返回一个类型为`b`的列表。

```Haskell
map :: (a -> b) -> [a] -> [b]
map _ []     = []
map g (x:xs) = g x : map g xs
```

我们也可以说`map`将函数`g :: a -> b`转换成汉纳树`map g :: [a] -> [b]`，换言之`map`是一个将函数转换为函数的**高阶函数**。由于它接受的类型是泛型，所以它本身不会决定存储在列表中的东西是什么类型，这完全由`g`来决定。因此，每个类型为`a->b`的函数在使用`map`后都会被以完全相同的逻辑转换为`[a] -> [b]`的函数。

这被称为**保持了一个列表的结构**，列表长度、元素相对位置是完全相同的。
<figure><img src="Haskell.asset/12.1%20map.svg"/></figure>


### 1.2 Functor 类族

如果有不同的结构保持映射函数，如何使用一个类族来保持他们的相似性呢？

```Haskell
map      :: (a -> b) ->      [a] ->      [b]
mapMaybe :: (a -> b) -> Maybe a  -> Maybe b
mapTree  :: (a -> b) -> Tree  a  -> Tree  b
```

之前提到的带类型变量的类族很难做到这一点。如果我们试图对`Maybe c`进行抽象，我们似乎无法为`map`操作写出正确的类型。我们需要能够以某种方式改变类型参数`c`。

```Haskell
class Mappable m where
  mapThing :: (a -> b) -> m -> m

instance Mappable (Maybe c) where
  mapThing :: (a -> b) -> Maybe c -> Maybe c
  mapThing = ...
```

好在Haskell中可以使用**类型构造函数**来作为类族的参数。来看一个标注库中的例子`Functor f`。这里不用传入一个`(Maybe a)`作为类型变量，而是直接使用构造函数`Maybe`，就得到了一个合适的类族！

```Haskell
class Functor f where
  fmap :: (a -> b) -> f a -> f b

instance Functor Maybe where
  -- In this instance, the type of fmap is:
  -- fmap :: (a -> b) -> Maybe a -> Maybe b
  fmap f Nothing = Nothing
  fmap f (Just x) = Just (f x)


```

而对于`map`，直接传列表的构造函数`[]`。

```Haskell
instance Functor [] where
  fmap = map
```

如果是`Tree`的话也类似的，传入构造函数，然后写`mapTree`的逻辑就行。

```Haskell
data Tree a = Leaf | Node a (Tree a) (Tree a)

instance Functor Tree where
  fmap _ Leaf = Leaf
  fmap f (Node val left right) = Node (f val) (fmap f left) (fmap f right)
```

!!! Note
    Functor这个词是从范畴论（以抽象的方法来处理数学概念，将这些概念形式化成一组组的“对象”及“态射”）里来的。

当有一个实例`Functor MyFun`的时候，就可以
    1. 将一个类型`X`映射到新的类型`MyFun`（由于`MyFun`是一个构造函数）。
    2. 将一个参数为`X`的函数`f`转换成参数为`MyFun X`的函数`fmap f`。
也就是说，我们不仅可以从**值**的层面上做映射，也可以从**类型**的层面上做映射。

!!! Tip
    如果经常使用`fmap` 的情况，可以使用中缀表达式`<$>`。
    ```Haskell
    (+1) <$> [1,2,3]    ==>  [2,3,4]
    not <$> Just False  ==>  Just True
    reverse . tail  $       "hello"       ==>  "olle"
    reverse . tail <$> Just "hello"       ==>  Just "olle"
    -- which is the same as
    fmap (reverse . tail) (Just "hello")  ==>  Just "olle"```

## 2 合法实例

`fmap`实际上不是对每个函数都能保持结构的，因此要特别注意作用在什么函数上。
之前所说的“保持结构”其实就是以下这两条函子规则
    1. `fmap id === id`
    2. `fmap (f.g) === fmap f. fmap g`
第一条规则就是函子将`id :: a -> a`映射到`id :: f a -> f a`。例如以下``famp id [1,2,3] = id [1,2,3] = [1,2,3]`。换言之，如果`fmap`作用的函数是恒等，那么结果也是恒等。

```Haskell
fmap id [1,2,3] ==> map id [1,2,3]
                ==> map id (1:[2,3])
                ==> id 1 : map id [2,3]
                ==> 1 : map id [2,3]
                ==> 1 : id 2 : map id [3]
                ==> 1 : 2 : id 3 : map id []
                ==> 1 : 2 : 3 : []
                === [1,2,3]
```

而对于第二条规则，考虑`fmap negate.(*2)`，对于` (negate.(*2))`和`(fmap negate . fmap (*2))`的结果是相同的。第二条规则实际上说的就是，先复合再作用，和分别作用再符合会得到相同的结果。

```Haskell
fmap (negate.(*2)) [1,2,3] 
        ==> map (negate.(*2)) [1,2,3]
        ==> (negate.(*2)) 1 : map (negate.(*2)) [2,3]
        ==> negate (1 * 2)  : map (negate.(*2)) [2,3]
        ==> -2 : map (negate.(*2)) [2,3]
        ==> -2 : (negate.(*2)) 2 : map (negate.(*2)) [3]
        ==> -2 : -4 : map (negate.(*2)) [3]
        ==> -2 : -4 : (negate.(*2)) 3 : map (negate.(*2)) []
        ==> -2 : -4 : -6 : []
        ==> [-2,-4,-6]

(fmap negate . fmap (*2)) [1,2,3] 
        ==> (map negate . map (*2)) [1,2,3]
        ==> map negate (map (*2) [1,2,3])
        ==> map negate [2,4,6]
        ==> [-2,-4,-6]
```

来看一个不能够保持结构的例子。以下的`badMap`中，如果列表长度长于2，将会舍弃掉第二个元素。

```Haskell
badMap :: (a -> b) -> [a] -> [b]
badMap f [] = []
badMap f (x:y:xs) = f x : badMap f xs
badMap f (x:xs) = f x : badMap f xs
```

对于`id`运算，显然这个`badMap id \= id`。

```Haskell
badMap id [1,2,3] ==> badMap id (1:2:[3])
                  ==> id 1 : badMap id [3]
                  ==> 1 : badMap id [3]
                  ==> 1 : badMap id (3:[])
                  ==> 1 : id 3 : badMap id []
                  ==> 1 : 3 : []
                  ==> [1,3]
```

!!! Note
    Haskell编译器不能分辨函子是否遵循了上面的规则！有其他的技术LiquidHaskell或者独立类型的语言可能可以强制实施函子规则，这样以后不合法的函子就不会被编译。
    
## 4 Kinds

`Functor`是**类型构造函数的类族**，如果尝试给一个类型来定义`Functor`实例，那么则会报错。

```Haskell
Prelude> instance Functor Int where

<interactive>:1:18: error:
    • Expected kind ‘* -> *’, but ‘Int’ has kind ‘*’
    • In the first argument of ‘Functor’, namely ‘Int’
      In the instance declaration for ‘Functor Int’
```

这个报错消息中提到了kinds。Kinds其实就是“类型的类型”（types of types）。类型是类似于`Int, Bool, Maybe Int`这样可以包含值的东西，他们的kind是`*`。而类型构造函数的kind看上去和函数相同，例如`Maybe`的kind是`*->*`。

```Haskell
Int :: * -- type 
Bool :: *
() :: *
[[Int]] :: *

--data 
[] :: * -> *
Maybe :: * -> *  -- type constructor
Either :: * -> * -> *

-- class
Num :: * -> Constraint
Eq :: * -> Constraint
Ord :: * -> Constraint
Monoid :: * -> Constraint
Semigroup :: * -> Constraint
Functor :: (* -> *) -> Constraint
Foldable :: (* -> *) -> Constraint
```

## 5 Foldable可折叠

很多函数签名中都有`Foldable`,在上面我们也看到了它的kind和Functor是类似的，接收一个函数签名。常见的有`List, Map, Array`等。

```Haskell
length :: Foldable t => t a -> Int
sum :: (Foldable t, Num a) => t a -> a
minimum :: (Foldable t, Ord a) => t a -> a
foldMap :: (Foldable t, Monoid m) => (a -> m) -> t a -> m
```

`Foldable`的核心就是一系列可以折叠的类，可以简单定义如下。当我们定义一个`Foldable`的实例的时候，只需要定义`foldr`就可以了。

```Haskell
class Foldable (t :: *->*) where
  foldr :: (a -> b -> b) -> b -> t a -> b
```

另一种思考`Foldable`的方式是**从左到右**来处理元素。换言之，如果`Functor`是容器的类，`Foldable`就是**有序容器**的类。

我们用一个`Pair`的例子来实现`Foldable`和`Functor`。

```Haskell
data Pair a = Pair a a
  deriving Show

instance Functor Pair where
  -- fmap f 将 f 应用到所有值上
  fmap f (Pair x y) = Pair (f x) (f y)

instance Foldable Pair where
  -- 就像是将foldr应用到一个长度为2的列表上
  foldr f initialValue (Pair x y) = f x (f y initialValue)

-- 这是一个使用到了这两个实例的例子
doubleAndCount :: (Functor f, Foldable f) => f Int -> Int
doubleAndCount = sum . fmap (*2)
```

在定义了这两个之后，几乎任何`list`可以用的函数它都能用。

```Haskell
fmap (+1) (Pair 3 6)   ==> Pair 4 7
fmap (+1) [3,6]        ==> [4,7]

foldr (*) 1 (Pair 3 6) ==> 18
foldr (*) 1 [3,6]      ==> 18

length (Pair 3 6)      ==> 2
length [3,6]           ==> 2

minimum (Pair 3 6)     ==> 3
minimum [3,6]          ==> 3

doubleAndCount (Pair 3 6)  ==> 18
doubleAndCount [3,6]       ==> 18
```
