# 常用库

## 1 Text库和ByteString库

之前所学的`String`其实是`Char`的别名，也就是字符的一个链表。实际使用起来其实是非常低效的，更高效的库有:
    - `Data.Text`，表示一系列Unicode字符，操作和`String`基本相同，只是更加高效。通常用来处理文本。
    - `Data.ByteString`，用来表示一系列字节，通常用来处理二进制数据。

另外，两种类型都有普通版本->strict（要求整个字符串一次装入内存）和lazy（能够使用一个小的内存空间来流传大于内存的字符串）和两种。导入分别是`import qualified Data.Text`和`import qualified Data.Text.Lazy`。所有模块的API几乎相同。

### 1.1 `Data.Text`例子

1. 使用`pack`函数将普通String转换成Text类型。

    ```Haskell
    Prelude> import qualified Data.Text as T
    Prelude T> :t T.pack 
    T.pack :: String -> T.Text
    Prelude T> phrase = T.pack "brevity is the soul of wit"
    Prelude T> :t phrase
    phrase :: T.Text
    Prelude T> phrase
    "brevity is the soul of wit"
    ```

2. 使用`Data.Text`中的不同函数

    ```Haskell
    Prelude T> :t T.length -- 长度
    T.length :: T.Text -> Int
    Prelude T> T.length phrase 
    26
    Prelude T> T.head phrase -- 取首
    'b'
    Prelude T> T.take 4 phrase -- 从头部取4长
    "brev"
    Prelude T> :t T.words -- 按空格拆分
    T.words :: T.Text -> [T.Text]
    Prelude T> T.words phrase
    ["brevity","is","the","soul","of","wit"]
    Prelude T> :t T.map -- 字符替换
    T.map :: (Char -> Char) -> T.Text -> T.Text
    Prelude T> T.map (\c -> if c=='o' then '0' else c) phrase
    "brevity is the s0ul 0f wit"
    ```

3. `Text`的`Monoid`实例（拼接操作）

    ```Haskell
    Prelude T> phrase <> phrase
    "brevity is the soul of witbrevity is the soul of wit"
    Prelude T> T.append phrase phrase -- T.append :: T.Text -> T.Text -> T.Text
    "brevity is the soul of witbrevity is the soul of wit"
    Prelude T> T.concat [phrase,phrase,phrase] -- T.concat :: [T.Text] -> T.Text
    "brevity is the soul of witbrevity is the soul of witbrevity is the soul of wit"
    ```

4. 递归中对`Text`对象进行模式匹配
    使用函数`T.uncons :: T.Text -> Maybe (Char, T.Text)`，将一个`Text`函数分为头字符、尾串。

   ```Haskell
    countLetter :: Char -> T.Text -> Int
    countLetter c t =
    case T.uncons t of
        Nothing -> 0
        Just (x,rest) -> (if x == c then 1 else 0) + countLetter c rest
   ```

5. 使用`Text`的lazy和strict变种

    `Strict`类型不能存储无限长字符。

    ```Haskell
    Prelude T> T.head (T.pack (repeat 'x'))
    -- 使用Strict版本的Text永不返回
    Prelude T> import qualified Data.Text.Lazy as TL
    Prelude T TL> TL.head (TL.pack (repeat 'x'))
    'x'
    -- 而使用lazy版本的可以
    ```

    另一个实际问题是，在使用库时，可能会误匹配strict和lazy的`Text`。通过toStrict或fromStrict可以在二者间进行转换。

## 1.2 `ByteString`例子

`ByteString`和`Text`提供的API几乎相同。需要注意的是`ByteString`是由`Word8`的值来构建的，而不是`Char`值。`Word8`使用一个0~255之间的值来表示一个字节，而不是某个具体字符。

```Haskell
Prelude Data.Word> import qualified Data.ByteString as B
Prelude Data.Word B> binary = B.pack [99,111,102,102,101,101]
Prelude Data.Word B> binary
"coffee"
ghci> binary = B.pack [99,111,102,102,101,253]
ghci> binary
"coffe\253"
```

这里值得注意的点是，`ByteString`对象也是以`String`的方式打印出来，不是本来存储的字符流。

!!! Note `Text`和`ByteString`的区别
    - 当我们对`Text`进行操作时，我们逐个字符进行操作，不管这些字符是什么，也不管它们是如何编码的。当我们对`ByteString`进行操作时，我们对字节进行操作，而不管这些字节代表什么。**其本质区别在于字节的分组和解释方式。**
    - 使用函数`Data.Text.Encoding.encodeUtf8 :: Text -> ByteString` 可以将`Text`对象中的字符转换成UTF-8编码的`ByteString`对象。
    - 处理ASCII文本的时候，每个字符可以被翻译成单个字节，那么使用这两种类型是完全等价的。在`Data.ByteString.Char8` 和 `Data.ByteString.Lazy.Char8`中提供了能够在`ByteStrings`中使用`Char`值而不是`Word8`值的方法。（一定要是ASCII!!）

## 2 Network库

在Haskell中关于网络的标准低级组件被称为`WAI`和`Warp`。

- `WAI`，即Web应用接口(Web Application Interface)，为我们提供了一种定义如何处理HTTP请求的方法。
- `Warp`是一个简单的HTTP服务器，运行我们用WAI定义的逻辑。


### 2.1 简单的HTTP服务器例子

```Haskell
module Examples.HelloServer where

import qualified Data.ByteString.Lazy.Char8 as BL
import Network.HTTP.Types.Status (status200)
import Network.Wai (Application, responseLBS)
import Network.Wai.Handler.Warp (run)

port :: Int
port = 3421

main :: IO ()
main = run port application

-- type Application = Request -> (Response -> IO ResponseReceived) -> IO ResponseReceived
application :: Application
application request respond =
  respond (responseLBS status200 [] (BL.pack "Hello World!"))
```

`Application`是一个类型别名，用于实现网络服务器的逻辑。`Warp`的`run`函数可以运行`Application`。

```Haskell
run :: Port -> Application -> IO ()
type Application = Request -> (Response -> IO ResponseReceived) -> IO ResponseReceived
```

`Application`是一个IO操作，它的参数是一个`Request`类型的请求，以及一个`IO`操作`reply :: Response -> IO ResponseReceived`（一个回调函数，允许我们回调到调用应用程序的库中）。`Application`操作必须产生与`reply`相同的特殊`ResponseReceived`类。

以上例子中的Server其实就是接受一个`Request` 和一个`Response`作为参数的`Application`。

在`WAI`中定义了很多类似于`Port`（`Int`的别名）, `Request`, `Response`, `Status`这样的类型来表示HTTP中的概念。比如`responseLBS`函数的定义：

```Haskell
responseLBS :: Status -> ResponseHeaders -> ByteString -> Response

-- Status在`Network.HTTP.Types.Status`定义
-- ResponseHeaders是[Header]的别名
-- ByteString是lazy的
-- Respnse在Netwok.WAI中定义
```

使用`pathInfo :: Request -> [Text]`能按照'/'字符分割出路径。

## 3 Database库: sqlite-simple

所有的函数都在Database.SQLite.Simple中。使用`open`函数能够打开一个数据库文件，它实际上是一个制造了一个`Connection`的`IO`操作。

```Haskell
Prelude> import Database.SQLite.Simple
Prelude Database.SQLite.Simple> :t open
open :: String -> IO Connection
Prelude Database.SQLite.Simple> db <- open "example.sqlite"
```

要运行一个SQL查询，可以使用IO操作`query_`，它接收一个`Connection`和一个`Query`(`Text`别名)，并产生一个`Result`列表。`query_`的结果类型是多态的：你可以从数据库中读取满足`FromRow`类型的任何类型。

```Haskell
Prelude Database.SQLite.Simple> :t query_
query_ :: FromRow r => Connection -> Query -> IO [r]
Prelude Database.SQLite.Simple> :info Query
newtype Query = Query {fromQuery :: Data.Text.Internal.Text}
    -- Defined in ‘Database.SQLite.Simple.Types’
    -- ... rest of output omitted
Prelude Database.SQLite.Simple> import qualified Data.Text as T
-- 读入一个SQL询问字符串
Prelude Database.SQLite.Simple T> q = Query (T.pack "SELECT 1;")
-- 指明读入一个Int，没有的话是会报错的
Prelude Database.SQLite.Simple T> res <- query_ db q :: IO [[Int]]
Prelude Database.SQLite.Simple T> res
[[1]]
```

sqlite-simole 定义了两种类型，分别是`FromField`和`FromRow`，以及以下一系列实例：

```Haskell
instance FromField Int
instance FromField Bool
instance FromField String
instance FromField Text
instance FromField a => FromRow [a]
instance (FromField a, FromField b) => FromRow (a,b)
instance (FromField a, FromField b, FromField c) => FromRow (a,b,c)

Prelude Database.SQLite.Simple T> q = Query (T.pack "SELECT 1, true, 'string';")
Prelude Database.SQLite.Simple T> query_ db q :: IO [(Int,Bool,String)]
[(1,True,"string")]
```

与`FromField,FromRow`相对应的是`ToField,ToRow`。

```Haskell
Prelude Database.SQLite.Simple T> input = (1,"hello") :: (Int,String)
Prelude Database.SQLite.Simple T> parameterized = Query (T.pack "SELECT ?+1, true, ?;")
Prelude Database.SQLite.Simple T> query db parameterized input :: IO [(Int,Bool,String)]
[(2,True,"hello")]
```
