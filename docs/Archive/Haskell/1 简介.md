# 函数式程序设计简介

## 核心概念

- **类型**：一些值的集合
- **函数**：带有一个或多个参数，并能返回一个结果的运算。

## Haskell -- 函数式程序设计语言

官方网站<http://www.haskell.org/>。

Haskell 是一种函数式程序设计语言，有多种实现。可以使用解释器进行交互运行，也可以编译程代码。Haskell toolchain官网上的工具链包括以下部分，通过官网上的GHCup可以对其进行统一安装。

1. 编译器和解释器GHC：

   - GHC(Glasgow Haskell compiler)是最广泛使用的Haskell编译器。GHC可以接受一个Haskell脚本（它们通常有一个.hs扩展名）并编译它。
   - GHCi(Glasgow Haskell compiler interactive)则是一个解释器，即GHC的交互模式。这意味着它像在纸上作计算一样一步步地计算表达式的值。你可以从你加载的脚本中调用函数，结果会立即显示。

2. 打包和发布工具Cabel：
    Cabal用于管理haskell的软件、库、安装包等。

3. 跨平台编译Stack：
    Stack类似Cmake工具，可以很方便的配置不同平台上的编译。

4. 语言服务器haskell-language-server （可选）：
   haskell-language-server 是一个语言服务器，供开发人员与他们的编辑器/IDE集成。

### Haskell 特征

- **Functional 函数式**： 程序的基本构成部分是函数。函数可以返回函数并以函数为参数。另外，Haskell中唯一的循环结构是递归。
- **Pure 纯净**： 没有副作用，副作用指的是诸如读取文件、打印出文本或改变全局变量。对于一个函数来说，所有的输入都在在它的参数中，所有输出都在它的返回值中。
- **Lazy 懒惰**： 惰性求值，值只有在被用到的时候才会被计算。因此能够支持无线数据类型。
- **Strongly type 强类型**：每个值和表达式都有类型，编译器会在编译期检查类型，因此类型错误不会发生在运行时。这也就说，Haskll中没有Python的`AttributeErrors`，C中的`segmentation faults`。
- **Type inferred 类型推断**：编译器会对类型自动推断，大部分函数都不需要显示给定类型(但是显式给出对debug总是好的)。
- **Garbage-collected 垃圾回收**
- **Compiled 编译**：虽然可以通过交互式ghci来实时运行，但是Haskell是编译语言。

### Haskell 语言特性

- **高阶函数**： 函数可以以领域另一个函数作为参数， 例如`map length ["abc","abcdef"] = [length "abc",length "abcdef"] = [3,6]`。
- **匿名函数**(aka lambdas)： 可以定义单次使用的函数，不用起名字`filter (\x -> length x > 1) ["abc","d","ef"] =  ["abc","ef"]`。
- **部分应用**： 可以通过在某个已有的函数上，给定部分参数的方式来定义一个新函数了。例如`map (*3) [1,2,3]`中给定了`*`一个变量`3`， 其效果为`[1*3,2*3,3*3]=[3,6,9]`。
- **代数数据类型**： 符合数据类型，通过结合其他类型形成的类型，包含不同的情况，例如`data Shape = Point | Rectangle Double Double | Circle Double`。
- **模式匹配**： 定义函数的时候可以根据输入变量的不同情况进行匹配， 例如以下代码分别匹配到`Shape`的不同情况

    ```Haskell
    area Point = 0
    area (Rectangle width height) = width * height
    area (Circle radius) = 2 * pi * radius
    ```

- **列表**： 有和Python类似的列表表达式，例如组合出奇数长度的名字，以及生成

    ```Haskell
    [whole | first <- ["Eva", "Mike"],
         last <- ["Smith", "Wood", "Odd"],
         let whole = first ++ last,
         even (length whole)]
    -- ["EvaSmith","EvaOdd","MikeWood"]
    ```

    生成素数列表，由于haskell的lazy特性，因此可以生成无限列表。

    ```Haskell
    primes = [ n | n <- [2..] , all (\k -> n `mod` k /= 0) [2..n `div` 2] ]
    take 10 primes -- [2,3,5,7,11,13,17,19,23,29]
    ```

- **参数化类型**：可以定义接受其他类型作为参数类型，例如列表`[a]`可以接受任何类型`a`作为输入，形成`[Int],[Bool]`等不同类型的列表。
- **类型类Type classes**： 一种多态，根据参数的类型给一个函数一个不同的实现。在[class](5.%20Class%E5%92%8CTypeclasses.md)中详细介绍。

## 什么是函数式程序

在函数式程序设计中，我们通过计算包含问题领域函数的表达式之值来解决问题。可以把一个函数式程序设计语言的实现看作与计算器类似的工具：输入一个表达式，系统计算出表达式的值。程序员的任务是编写将问题建模后的函数。

!!! [note]
    **函数程序** = 一系列函数 + 值的定义
    **定义**： 将一个名称(name)和一个类型(type)绑定。
    ```Haskell
        name :: type
        name = expression
```

## Haskell程序结构

```Haskell
module Gold where           -- 模块声明
-- The golden ratio         -- 注释符号
import Data.List            -- 模块导入

phi :: Double               -- 变量类型注解(aka类型签名,非必需)
phi = (sqrt 5 + 1) / 2      -- 变量赋值(必需)

polynomial :: Double -> Double  -- 函数类型注解
polynomial x = x^2 - x - 1      -- 函数等式

main = do                   -- main函数体
  print (polynomial phi)    -- 函数调用
  print (f phi)
```

### 表达式和类型

Haskell程序中几乎所有的东西都是表达式，没有像Python、Java或C那样的语句（**表达式 = 值 + 类型**）。比如以下的例子

| 表达式       | 类型   | 值     |
| ------------ | ------ | ------ |
| True         | Bool   | True   |
| not True     | Bool   | False  |
| "as" ++ "df" | [Char] | "asdf" |

变量签名型如`variable :: Type`。虽然被叫做变量，但实际上就是一个定义，它的值是不能被改变的。或者说Haskell中的变量实际上就是指明了一个值。

函数签名型如`function :: Type1 -> Type2 -> ... -> ReturnType`，其中每个`Type`是一个输入参数的类型(可以有多个)，而最后一个`ReturnType`则是返回值的类型。它们之间均使用`(->)`连接，返回值只能有一个。

### 函数调用及调用顺序

没有特殊语法，把参数直接放在函数名之后，空格隔开。

| Haskell | C++/Python |
| ------- | ---------- |
| f 1     | f(1)       |
| f 1 2   | f(1,2)     |

**括号**可以用来组合表达式，没有括号的话函数也会被视为参数。

| Haskell   | C++/Python |
| --------- | ---------- |
| g h f 1   | g(h,f,1)   |
| g h (f 1) | g(h,f(1))  |
| g (h f 1) | g(h(f,1))  |

函数调用的绑定比**运算符**更紧。

| Haskell     | C++/Python  |
| ----------- | ----------- |
| a + b       | a + b       |
| f a + g b   | f(a) + g(b) |
| f (a + g b) | f(a+g(b))   |

!!! Note 
    Haskell中函数应用向左联系，也就是说`f g x y =  (((f g) x) y)`

### 关于缩进的说明

Haskell中缩进很重要，但是规则比较难以描述，基本上来说只要做到

  1. 同一组的表达式从同一列开始
  2. 如果表达式必须被分成多行，那么后面的行数应该增加缩进。

举几个比较简单的例子，以下这些是正确范例

```Haskell
i x = let y = x+x+x+x+x+x in div y 5

-- let and in are grouped together, an expression is split
j x = let y = x+x+x
              +x+x+x
      in div y 5

-- the definitions of a and b are grouped together
k = a + b
  where a = 1
        b = 1

l = a + b
  where
    a = 1
    b = 1
```

而以下则无法通过编译

```Haskell
-- let..in被分成多行，但是没有缩进
i x = let y = x+x+x+x+x+x
in div y 5

-- y的表达式被分成多行，但是没有缩进
j x = let y = x+x+x
      +x+x+x
      in div y 5

-- where中定义的a和b没有对齐
k = a + b
  where a = 1
      b = 1

-- 另一种没有对齐
l = a + b
  where
    a = 1
     b = 1

-- where本身是l = a+b的一部分，需要进行缩进
l = a + b
where
  a = 1
  b = 1
```
